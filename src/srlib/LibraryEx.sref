//FROM Library
$EXTERN FOpen, FClose, FReadLine, FWriteLine, Add, Sub, IntFromStr, Arg,
  SymbCompare, FReadBytes, FWriteBytes;

// Расширения библиотеки и функции, реализуемые
// средствами самого Простого Рефала

$ENTRY Apply {
  s.Fn e.Argument = <s.Fn e.Argument>;

  (t.Closure e.Bounded) e.Argument =
    <Apply t.Closure e.Bounded e.Argument>;
}

$ENTRY Map {
  t.Fn t.Next e.Tail = <Apply t.Fn t.Next> <Map t.Fn e.Tail>;

  t.Fn = ;
}

$ENTRY Reduce {
  t.Fn t.Acc t.Next e.Tail =
    <Reduce
      t.Fn <Apply t.Fn t.Acc t.Next> e.Tail
    >;

  t.Fn t.Acc = t.Acc;
}

$ENTRY Fetch {
  e.Argument t.Function =
    <Apply t.Function e.Argument>;
}

$ENTRY Y {
  s.Y-Func = { e.Arg = <<s.Y-Func <Y s.Y-Func>> e.Arg>; };
}

$ENTRY MapReduce {
  t.Fn t.Acc e.Tail =
    <DoMapReduce t.Fn t.Acc () e.Tail>;
}

DoMapReduce {
  t.Fn t.Acc (e.Scanned) t.Next e.Tail =
    <DoMapReduce
      t.Fn
      <MapReduce-AddScanned
        <Apply t.Fn t.Acc t.Next> (e.Scanned)
      >
      e.Tail
    >;

  t.Fn t.Acc (e.Scanned) = t.Acc e.Scanned;
}

MapReduce-AddScanned {
  t.Acc e.StepScanned (e.Scanned) =
    t.Acc (e.Scanned e.StepScanned);
}

$ENTRY UnBracket {
  (e.Expr) = e.Expr;
}

$ENTRY DelAccumulator {
  t.Acc e.Tail = e.Tail;
}

$ENTRY LoadFile {
  e.FileName =
    <DoLoadFile
      <FReadLine <FOpen 'r' e.FileName>>
    >;
}

DoLoadFile {
  t.File 0 = <FClose t.File>;

  t.File e.Line 0 = (e.Line) <FClose t.File>;

  t.File e.Line =
    (e.Line)
    <DoLoadFile
      <FReadLine t.File>
    >;
}

$ENTRY SaveFile {
  (e.FileName) e.Lines =
    <SaveFile-Handle <FOpen 'w' e.FileName> e.Lines>;
}

$ENTRY AppendFile {
  (e.FileName) e.Lines =
    <SaveFile-Handle <FOpen 'a' e.FileName> e.Lines>;
}

SaveFile-Handle {
  s.FileHandle e.Lines =
    <FClose
      <Reduce
        WriteBracketLine
        s.FileHandle
        e.Lines
      >
    >;
}

$ENTRY LoadBytes {
  e.FileName =
    <DoLoadBytes
      <FOpen ('rb') e.FileName>
    >;
}

DoLoadBytes {
  s.FileHandle =
    <Fetch
      <FReadBytes s.FileHandle 4096>
      {
        /* пусто */ = <FClose s.FileHandle>;
        e.Bytes = e.Bytes <DoLoadBytes s.FileHandle>;
      }
    >;
}

$ENTRY SaveBytes {
  (e.FileName) e.Bytes =
    <SaveBytes-Handle <FOpen ('wb') e.FileName> e.Bytes>;
}

$ENTRY AppendBytes {
  (e.FileName) e.Bytes =
    <SaveBytes-Handle <FOpen ('ab') e.FileName> e.Bytes>;
}

SaveBytes-Handle {
  s.FileHandle e.Bytes =
    <FWriteBytes s.FileHandle e.Bytes>
    <FClose s.FileHandle>;
}

WriteBracketLine {
  t.File (e.Line) = <FWriteLine t.File e.Line>;
}

$ENTRY Inc {
  s.Num = <Add s.Num 1>;
}

$ENTRY Dec {
  s.Num = <Sub s.Num 1>;
}

FastIntFromStr-Guard {
  #Success s.Number = s.Number;
}

$ENTRY FastIntFromStr {
  e.Digits =
    <FastIntFromStr-Guard
      <IntFromStr e.Digits>
    >;
}

$ENTRY ArgList {
  = <DoArgList 0>;
}

DoArgList {
  s.Next = <SwDoArgList <Inc s.Next> <Arg s.Next>>;
}

SwDoArgList {
  s.Next = ;

  s.Next e.Arg = (e.Arg) <DoArgList s.Next>;
}

$ENTRY TermCompare {
  (e.Left) (e.Right) = <Lexicon (e.Left) (e.Right)>;

  (e.Left) s.Right = '>';
  s.Left (e.Right) = '<';

  s.Left s.Right = <SymbCompare s.Left s.Right>;
}

Lexicon {
  (t.FirstLeft e.Left) (t.FirstRight e.Right) =
    <Lexicon-Aux
      <TermCompare t.FirstLeft t.FirstRight>
      (e.Left) (e.Right)
    >;

  () () = '=';

  () (e.Right) = '<';
  (e.Left) () = '>';
}

Lexicon-Aux {
  '<' (e.Left) (e.Right) = '<';
  '=' (e.Left) (e.Right) = <Lexicon (e.Left) (e.Right)>;
  '>' (e.Left) (e.Right) = '>';
}

$ENTRY TermCompare-T {
  (e.Left) (e.Right) = <Lexicon-T () (e.Left) () (e.Right)>;

  (e.Left) s.Right = '>' (e.Left) s.Right;
  s.Left (e.Right) = '<' s.Left (e.Right);

  s.Left s.Right = <SymbCompare s.Left s.Right> s.Left s.Right;
}

Lexicon-T {
  (e.ScannedLeft) (t.FirstLeft e.Left)
  (e.ScannedRight) (t.FirstRight e.Right) =
    <Lexicon-T-Aux
      (e.ScannedLeft) (e.ScannedRight)
      <TermCompare-T t.FirstLeft t.FirstRight>
      (e.Left) (e.Right)
    >;

  (e.ScannedLeft) () (e.ScannedRight) () =
    '=' (e.ScannedLeft) (e.ScannedRight);

  (e.ScannedLeft) () (e.ScannedRight) (e.Right) =
    '<' (e.ScannedLeft) (e.ScannedRight e.Right);

  (e.ScannedLeft) (e.Left) (e.ScannedRight) () =
    '>' (e.ScannedLeft e.Left) (e.ScannedRight);
}

Lexicon-T-Aux {
  (e.ScannedLeft) (e.ScannedRight)
  '<' t.NextLeft t.NextRight (e.Left) (e.Right) =
    '<' (e.ScannedLeft t.NextLeft e.Left)
    (e.ScannedRight t.NextRight e.Right);

  (e.ScannedLeft) (e.ScannedRight)
  '=' t.NextLeft t.NextRight (e.Left) (e.Right) =
    <Lexicon-T
      (e.ScannedLeft t.NextLeft) (e.Left)
      (e.ScannedRight t.NextRight) (e.Right)
    >;

  (e.ScannedLeft) (e.ScannedRight)
  '>' t.NextLeft t.NextRight (e.Left) (e.Right) =
    '>' (e.ScannedLeft t.NextLeft e.Left)
    (e.ScannedRight t.NextRight e.Right);
}

$ENTRY Trim {
  e.Line = <Trim-L <Trim-R e.Line>>;
}

$ENTRY Trim-L {
  ' ' e.Line = <Trim-L e.Line>;
  '\t' e.Line = <Trim-L e.Line>;
  '\r' e.Line = <Trim-L e.Line>;
  '\n' e.Line = <Trim-L e.Line>;
  e.Line = e.Line;
}

$ENTRY Trim-R {
  e.Line ' ' = <Trim-R e.Line>;
  e.Line '\t' = <Trim-R e.Line>;
  e.Line '\r' = <Trim-R e.Line>;
  e.Line '\n' = <Trim-R e.Line>;
  e.Line = e.Line;
}

/**
  <Pipe s.F1 s.F2 ... s.Fn> == s.Compose

  <s.Compose e.X> == <s.Fn ... <s.F2 <s.F1 e.X>>...>

  Seq (deprecated) is synonim for Pipe
*/
$ENTRY Pipe {
  t.Func = t.Func;

  t.Func e.Funcs =
    {
      e.Arg = <Fetch <Apply t.Func e.Arg> <Pipe e.Funcs>>;
    };

  /* пусто */ = { e.Arg = e.Arg; };
}

/**
  Seq is deprecated, use Pipe
*/
$ENTRY Seq { e.Funcs = <Pipe e.Funcs>; }

$ENTRY Sort {
  /* пусто */ = /* пусто */;

  t.Pivot e.Items =
    <Fetch
      e.Items
      <Pipe
        (Reduce
          {
            ((e.Less) (e.Equal) (e.Greater)) t.Next =
              <Fetch
                <TermCompare t.Next t.Pivot>
                {
                  '<' = ((e.Less t.Next) (e.Equal) (e.Greater));
                  '=' = ((e.Less) (e.Equal t.Next) (e.Greater));
                  '>' = ((e.Less) (e.Equal) (e.Greater t.Next));
                }
              >;
          }
          ( (/* less */) (t.Pivot /* equal */) (/* greater */) )
        )
        {
          ((e.Less) (e.Equal) (e.Greater)) =
            <Sort e.Less> e.Equal <Sort e.Greater>;
        }
      >
    >;
}

$ENTRY Min {
  t.X t.Y =
    <Fetch
      <TermCompare t.X t.Y>
      {
        '<' = t.X;
        s.Other = t.Y;
      }
    >;

  t.First e.Tail =
    <Reduce Min t.First e.Tail>;
}

$ENTRY Max {
  t.X t.Y =
    <Fetch
      <TermCompare t.X t.Y>
      {
        '>' = t.X;
        s.Other = t.Y;
      }
    >;

  t.First e.Tail =
    <Reduce Max t.First e.Tail>;
}
