$INCLUDE "LibraryEx";

*$FROM TreeUtils
$EXTERN AddSuffix, ExtractVariables, CreateSpecPatternForContext,
  CreateContext, ExtractVariables-Expr, NewVarName;


/**
  <Pass-UnCondition s.MarkupContext t.Unit*> == t.Unit*

  s.MarkupContext ::= MarkupContext | NoMarkupContext
*/
$ENTRY Pass-UnCondition {
  s.MarkupContext e.Units
    = <Map (&UnCondition-Unit s.MarkupContext) e.Units>;
}

** НАМЕРЕННО ОТКАЗЫВАЕМСЯ ОТ ОТСЛЕЖИВАНИЯ КОПИРОВАНИЯ ПОВТОРНЫХ ПЕРЕМЕННЫХ

/**
  <UnCondition-Unit s.MarkupContext (e.Name) s.Scope e.Sentences>
    == e.NewFunctions
*/
UnCondition-Unit {
  s.MarkupContext (Function s.Scope (e.Name) Sentences e.Sentences)

    = 1 : s.SkipNo
    = /* пусто */ : e.RemNames
    = /* пусто */ : e.NewFunctions
    = <MapAccum
        (&UnCondition-Unit-Sentence e.Name s.MarkupContext)
        (s.SkipNo e.RemNames (e.NewFunctions))
        e.Sentences
      >
    : (s.SkipNo^ e.RemNames^ (e.NewFunctions^)) e.Sentences^

    = (Function s.Scope (e.Name) Sentences e.Sentences)
      <UnCondition-RemFunctions 1 (e.RemNames) e.Sentences>
      <Map (&UnCondition-Unit s.MarkupContext) e.NewFunctions>;

  s.MarkupContext t.Other = t.Other;
}

UnCondition-Unit-Sentence {
  e.Name s.MarkupContext (s.SkipNo e.RemNames (e.NewFunctions)) (e.Sentence)
    , e.Sentence : (e._) (Condition (e.ConditionName) (e._) (e._)) e._
    = <AddSuffix e.Name ('*' s.SkipNo)> : e.RemName
    = <UnCondition-Sentence s.MarkupContext (e.RemName) e.Sentence>
    : (e.Sentence^) e.NewSentenceFunctions
    = (
        <Inc s.SkipNo> e.RemNames (e.RemName)
        (e.NewFunctions e.NewSentenceFunctions)
      )
      (e.Sentence);

  e.Name s.MarkupContext (s.SkipNo e.RemNames (e.NewFunctions)) (e.Sentence)
    = (<Inc s.SkipNo> e.RemNames '-' (e.NewFunctions)) (e.Sentence);
}

UnCondition-RemFunctions {
  s.SkipNo (t.RemainderName e.RemNames) t.Skip e.Sentences
    = <Fetch
        t.RemainderName
        {
          (e.RemainderName)
            = (Function GN-Local (e.RemainderName) Sentences e.Sentences);

          '-' = /* пусто */;
        }
      >
      <UnCondition-RemFunctions <Inc s.SkipNo> (e.RemNames) e.Sentences>;

  s.SkipNo (/* пусто */) /* пусто */ = /* пусто */;
}

/*
  Схема нумерации замыканий:
  ?0 - функция проверки,
  ?1 - функция продолжения (вместо них генерируется *n)
  ?n, n - чётное, - функция forward,
  ?n, n - нечётное, - функция next.
*/

PatternSteps {
  (e.UsedVariables) e.Pattern
    = <DoPatternSteps
        (e.UsedVariables) (<gen_e__ /* bound variables */>)
        <gen_e__ (HOLE e.Pattern)>
      >;
}

gen_e__ { e.arg = e.arg }

DoPatternSteps {
  t.UsedVariables (e.BoundEVariables) e.HolesAndItems
    , e.HolesAndItems
    : {
        /* Жёсткие элементы */
        e.HolesAndItems-B (HOLE t.Item e.Pattern) e.HolesAndItems-E
          , <HardItem (e.BoundEVariables) t.Item> : True
          = <DoPatternSteps
              t.UsedVariables (e.BoundEVariables)
              e.HolesAndItems-B t.Item (HOLE e.Pattern) e.HolesAndItems-E
            >;

        e.HolesAndItems-B (HOLE e.Pattern t.Item) e.HolesAndItems-E
          , <HardItem (e.BoundEVariables) t.Item> : True
          = <DoPatternSteps
              t.UsedVariables (e.BoundEVariables)
              e.HolesAndItems-B (HOLE e.Pattern) t.Item e.HolesAndItems-E
            >;

        /* Закрытая e-переменная */
        e.HolesAndItems-B (HOLE (Var 'e' e.Index)) e.HolesAndItems-E
          = <DoPatternSteps
              t.UsedVariables (e.BoundEVariables (e.Index))
              e.HolesAndItems-B (Var 'e' e.Index) e.HolesAndItems-E
            >;

         /* Пустая дырка */
         e.HolesAndItems-B (HOLE /* пусто */) e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables)
               e.HolesAndItems-B e.HolesAndItems-E
             >;

         /* Скобки */
         e.HolesAndItems-B
         (HOLE (Brackets e.InBrackets) e.Pattern)
         e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables)
               e.HolesAndItems-B
               (LeftBracket)
               (HOLE e.InBrackets)
               (RightBracket)
               (HOLE e.Pattern)
               e.HolesAndItems-E
             >;

         e.HolesAndItems-B
         (HOLE e.Pattern (Brackets e.InBrackets))
         e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables)
               e.HolesAndItems-B
               (HOLE e.Pattern)
               (LeftBracket)
               (HOLE e.InBrackets)
               (RightBracket)
               e.HolesAndItems-E
             >;

         e.HolesAndItems-B
         (HOLE (ADT-Brackets (e.Name) e.InBrackets) e.Pattern)
         e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables)
               e.HolesAndItems-B
               (LeftADT e.Name)
               (HOLE e.InBrackets)
               (RightADT)
               (HOLE e.Pattern)
               e.HolesAndItems-E
             >;

         e.HolesAndItems-B
         (HOLE e.Pattern (ADT-Brackets (e.Name) e.InBrackets))
         e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables)
               e.HolesAndItems-B
               (HOLE e.Pattern)
               (LeftADT e.Name)
               (HOLE e.InBrackets)
               (RightADT)
               e.HolesAndItems-E
             >;

         /* Открытая e-переменная */
         e.HolesAndItems-B
         (HOLE (Var 'e' e.Index) e.Pattern)
         e.HolesAndItems-E
           = <DoPatternSteps
               t.UsedVariables (e.BoundEVariables (e.Index))
               e.HolesAndItems-B
               (OpenE 'e' e.Index)
               (HOLE e.Pattern)
               e.HolesAndItems-E
             >
             (
               <Map
                 &BindBrackets
                 <T1 t.UsedVariables e.HolesAndItems>
                 <T2 t.UsedVariables e.HolesAndItems>
                 <T3 t.UsedVariables e.HolesAndItems>
                 <T4 t.UsedVariables e.HolesAndItems>
                 <T5 t.UsedVariables e.HolesAndItems>
                 <T5-sub t.UsedVariables e.Index>
                 <T6 t.UsedVariables e.HolesAndItems>
                 <T7 t.UsedVariables e.HolesAndItems>
               >
             );

           /* Конец разбора */
           e.OnlyItems
             = (<BindBrackets <T0 e.OnlyItems>>);
      };
}

HardItem {
  (e.BoundEVariables) (Symbol s.Type e.Info) = True;

  (e.BoundEVariables) (Var 's' e.Index) = True;
  (e.BoundEVariables) (Var 't' e.Index) = True;

  (e.BoundEVariables-B (e.Index) e.BoundEVariables-E)
  (Var 'e' e.Index) = True;

  (e.BoundEVariables) (Var 'e' e.Index) = False;
  (e.BoundEVariables) (Brackets e.Pattern) = False;
  (e.BoundEVariables) (ADT-Brackets (e.Name) e.Pattern) = False;
}

BindBrackets {
  (e.Expr) = (<DoBindBrackets ('$') e.Expr>);
}

DoBindBrackets {
  (t.MultiBracket e.Scanned) (Symbol s.Type e.Info) e.Tail
    = <DoBindBrackets (t.MultiBracket e.Scanned (Symbol s.Type e.Info)) e.Tail>;

  (t.MultiBracket e.Scanned) (Var s.Type e.Index) e.Tail
    = <DoBindBrackets
        (t.MultiBracket e.Scanned (Var s.Type e.Index)) e.Tail
      >;

  (t.MultiBracket e.Scanned) (OpenE s.Type e.Index) e.Tail
    = <DoBindBrackets
        (t.MultiBracket e.Scanned (Brackets (Var s.Type e.Index)))
        e.Tail
      >;

  (t.MultiBracket e.Scanned) (Brackets e.InBrackets) e.Tail
    = <DoBindBrackets
        (t.MultiBracket e.Scanned (Brackets e.InBrackets)) e.Tail
      >;

  (t.MultiBracket e.Scanned) (LeftBracket) e.Tail
    = <DoBindBrackets ((t.MultiBracket e.Scanned)) e.Tail>;

  ((t.MultiBracket e.Scanned) e.InBrackets) (RightBracket) e.Tail
    = <DoBindBrackets
        (t.MultiBracket e.Scanned (Brackets e.InBrackets)) e.Tail
      >;

  (t.MultiBracket e.Scanned) (LeftADT e.Name) e.Tail
    = <DoBindBrackets ((t.MultiBracket e.Scanned) (e.Name)) e.Tail>;

  ((t.MultiBracket e.Scanned) (e.Name) e.InBrackets) (RightADT) e.Tail
    = <DoBindBrackets
        (t.MultiBracket e.Scanned (ADT-Brackets (e.Name) e.InBrackets)) e.Tail
      >;

  ('$' e.Scanned) = e.Scanned;
}

/*
  Смысл функций T1…T7 поясняется в «Подход к преобразованию условий.md».
*/

T1 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    , <NewVarName t.UsedVars1 't' e.Index '_next'> : t.UsedVars2 e.tX_next
    , <NewVarName t.UsedVars2 'e' e.Index '_rest'> : t.UsedVars3 e.eX_rest
    = (
        e.Items
        (Brackets (Var e.eX_fix))
        (Var e.tX_next)
        (Var e.eX_rest)
        <PassiveHoles t.UsedVars3 e.ItemsAndHoles>
      );
}

PassiveHoles {
  t.UsedVariables
  e.Items (HOLE (Var 'e' e.Index) e.Pattern)
  e.ItemsAndHoles
    , <NewVarName t.UsedVariables 'e' e.Index '_rest'> : t.NewVars e.eX_rest
    = e.Items
      (Var e.eX_rest)
      <PassiveHoles t.NewVars e.ItemsAndHoles>;

  t.UsedVariables e.Items = e.Items;
}

T2 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    , <NewVarName t.UsedVars1 't' e.Index '_next'> : t.UsedVars2 e.tX_next
    , <NewVarName t.UsedVars2 'e' e.Index '_rest'> : t.UsedVars3 e.eX_rest
    = (
        e.Items
        (Brackets (Var e.eX_fix) (Var e.tX_next))
        (Var e.eX_rest)
        <PassiveHoles t.UsedVars3 e.ItemsAndHoles>
      );
}

T3 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    = (
        e.Items
        (Brackets (Var e.eX_fix))
        <PassiveHoles t.UsedVars1 e.ItemsAndHoles>
      );
}

T4 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    = (
        e.Items (Var e.eX_fix)
        <PassiveHoles t.UsedVars1 e.ItemsAndHoles>
      );
}

T5 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <T5-sub t.UsedVars e.Index> : (t.SourceVar t.eX_fix t.eX_var)
    = (
        e.Items (Brackets t.eX_fix) t.eX_var
        <Substitute
          e.Pattern <T5-UnHole e.ItemsAndHoles> (t.SourceVar t.eX_fix t.eX_var)
        >
      );
}

T5-UnHole {
  e.Items (HOLE e.Pattern) e.ItemsAndHoles
    = e.Items e.Pattern <T5-UnHole e.ItemsAndHoles>;

  e.Items = e.Items;
}

T5-sub {
  t.UsedVars e.Index
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    , <NewVarName t.UsedVars 'e' e.Index '_var'> : t.UsedVars2 e.eX_var
    = ((Var 'e' e.Index) (Var e.eX_fix) (Var e.eX_var));
}

Substitute {
  (Var s.Type e.Index) e.Tail ((Var s.Type e.Index) e.Expr)
    = e.Expr
      <Substitute e.Tail ((Var s.Type e.Index) e.Expr)>;

  (Brackets e.InBrackets) e.Tail (t.Variable e.Expr)
    = (Brackets <Substitute e.InBrackets (t.Variable e.Expr)>)
      <Substitute e.Tail (t.Variable e.Expr)>;

  (ADT-Brackets (e.Name) e.InBrackets) e.Tail (t.Variable e.Expr)
    = (ADT-Brackets (e.Name) <Substitute e.InBrackets (t.Variable e.Expr)>)
      <Substitute e.Tail (t.Variable e.Expr)>;

  (CallBrackets e.InBrackets) e.Tail (t.Variable e.Expr)
    = (CallBrackets <Substitute e.InBrackets (t.Variable e.Expr)>)
      <Substitute e.Tail (t.Variable e.Expr)>;

  (ClosureBrackets e.InBrackets) e.Tail (t.Variable e.Expr)
    = (ClosureBrackets <Substitute e.InBrackets (t.Variable e.Expr)>)
      <Substitute e.Tail (t.Variable e.Expr)>;

  t.OtherItem e.Tail (t.Variable e.Expr)
    = t.OtherItem <Substitute e.Tail (t.Variable e.Expr)>;

  /* пусто */ (t.Variable e.Expr)
    = /* пусто */;
}

T6 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    , <NewVarName t.UsedVars1 'e' e.Index '_rest'> : t.UsedVars2 e.eX_rest
    = (
        e.Items
        (Brackets (Var e.eX_fix))
        (Var e.eX_rest)
        <PassiveHoles t.UsedVars2 e.ItemsAndHoles>
      );
}

T7 {
  t.UsedVars
  e.Items (HOLE (Var 'e' e.Index) e.Pattern) e.ItemsAndHoles
    , <NewVarName t.UsedVars 'e' e.Index '_fix'> : t.UsedVars1 e.eX_fix
    , <NewVarName t.UsedVars1 'e' e.Index '_rest'> : t.UsedVars2 e.eX_rest
    = (
        e.Items
        (Var e.eX_fix) (Var e.eX_rest)
        <PassiveHoles t.UsedVars2 e.ItemsAndHoles>
      );
}

T0 {
  e.Items = (<DoT0 e.Items>);
}

DoT0 {
  (OpenE 'e' e.Index) e.Tail
    = (Brackets (Var 'e' e.Index)) <DoT0 e.Tail>;

  (Brackets e.InBrackets) e.Tail = (Brackets <DoT0 e.InBrackets>) <DoT0 e.Tail>;
  (ADT-Brackets (e.Name) e.InBrackets) e.Tail
    = (ADT-Brackets (e.Name) <DoT0 e.InBrackets>) <DoT0 e.Tail>;

  t.OtherItem e.Tail = t.OtherItem <DoT0 e.Tail>;

  /* пусто */ = /* пусто */;
}

/**
  <UnCondition-Sentence s.MarkupContext (e.ContName) e.Sentence>
    == (e.TransformedSentence) e.SubFunctions
*/
UnCondition-Sentence {
  s.MarkupContext t.ContName e.Sentence
    , e.Sentence
    : (e.Pattern)
      (Condition (e.ConditionName) (e.CondResult) (e.CondPattern))
      e.SentenceTail
    , e.ConditionName '?' 0 : e.CheckFunction
    , <ExtractVariables (e.Sentence)> : e.SentenceVariables
    , <ConditionContext s.MarkupContext e.Pattern> : e.Context
    , <PatternSteps (e.SentenceVariables) e.Pattern> : e.PatternSteps
    , <OpenEChain
        (e.ConditionName) (e.SentenceVariables) t.ContName (e.CheckFunction)
        (e.Context) (e.CondResult)
        e.PatternSteps
      >
    : (e.ContSentence) e.OpenEFunctions
    , (Function GN-Local (e.CheckFunction) Sentences
        ((e.Context e.CondPattern) e.SentenceTail)
        e.ContSentence
      )
    : t.CheckFunction
    = (
        (e.Pattern)
        (
          (CallBrackets
            (Symbol Name e.CheckFunction) e.Context e.CondResult
          )
        )
      )
      (Drive e.CheckFunction)
      (Spec (e.CheckFunction) <CreateSpecPatternForContext e.Context>)
      t.CheckFunction e.OpenEFunctions;
}

ConditionContext {
  s.MarkupContext e.Pattern
    = <CreateContext s.MarkupContext <Unique <ExtractVariables-Expr e.Pattern>>>
}

OpenEChain {
  (e.ConditionName) (e.VarNames) (e.ContName)
  (e.CheckFunction) (e.Context) (e.CondResult)
  ((e.T0)) /* пусто */
    , <NewVarName (e.VarNames) 'eOther' 0> : (e.NewVarNames) e.eOther
    = (
        (
          (e.Context (Var e.eOther))
          ((CallBrackets (Symbol Name e.ContName) e.T0))
        )
      )
      /* пусто */;

  (e.ConditionName) (e.VarNames) t.ContName
  (e.CheckFunction) (e.Context) (e.CondResult)
  ((e.T0)) e.Templates
    , e.ConditionName '?' 2 : e.NextForward
    , <DoOpenEChain
        (e.ConditionName) 2 (e.NextForward) t.ContName
        (e.CheckFunction) (e.Context e.CondResult)
        (/* generated */) e.Templates
      >
    : e.NewFunctions
    , <NewVarName (e.VarNames) 'eOther' 0> : (e.NewVarNames) e.eOther
    = (
        (
          (e.Context (Var e.eOther))
          ((CallBrackets (Symbol Name e.NextForward) e.T0))
        )
      )
      e.NewFunctions;
}

DoOpenEChain {
  (e.ConditionName) s.CurrentForwardId (e.CurrentForward) t.ContName
  (e.CheckFunction) (e.CheckFunctionArg)
  (e.GeneratedFunctions) (e.TemplateSet) /* пусто */
    , <MakeOpenE-ForwardNext
        (e.ConditionName) s.CurrentForwardId (e.CurrentForward) t.ContName
        (e.CheckFunction) (e.CheckFunctionArg)
        e.TemplateSet
      >
    : e.NewFunctions
    = e.GeneratedFunctions e.NewFunctions;

  (e.ConditionName) s.CurrentForwardId (e.CurrentForward) t.ContName
  (e.CheckFunction) (e.CheckFunctionArg)
  (e.GeneratedFunctions) (e.TemplateSet) e.TemplateSets
    , <+ 2 s.CurrentForwardId> : s.NextForwardId
    , e.ConditionName '?' s.NextForwardId : e.NextForward
    , <MakeOpenE-ForwardNext
        (e.ConditionName) s.CurrentForwardId (e.CurrentForward) (e.NextForward)
        (e.CheckFunction) (e.CheckFunctionArg)
        e.TemplateSet
      >
    : e.NewFunctions
    = <DoOpenEChain
        (e.ConditionName) s.NextForwardId (e.NextForward) t.ContName
        (e.CheckFunction) (e.CheckFunctionArg)
        (e.GeneratedFunctions e.NewFunctions) e.TemplateSets
      >;
}

MakeOpenE-ForwardNext {
  (e.ConditionName) s.CurrentForwardId (e.CurrentForwardName) (e.ContName)
  (e.CheckFunction) (e.CheckFunctionArg)
  (e.T1) (e.T2) (e.T3) (e.T4) (e.T5) (e.T5-sub) (e.T6) (e.T7)
    , e.ConditionName '?' <+ 1 s.CurrentForwardId> : e.NextName
    , <Substitute e.CheckFunctionArg (e.T5-sub)> : e.CheckFuncArgSubst
    = (Function GN-Local (e.CurrentForwardName) Sentences
        ((e.T1) ((CallBrackets (Symbol Name e.NextName) e.T2)))
        ((e.T3) ((CallBrackets (Symbol Name e.ContName) e.T4)))
      )
      (Drive e.CurrentForwardName)
      (Function GN-Local (e.NextName) Sentences
        (
          (e.T5)
          ((CallBrackets (Symbol Name e.CheckFunction) e.CheckFuncArgSubst))
        )
        ((e.T6) ((CallBrackets (Symbol Name e.ContName) e.T7)))
      );
}
