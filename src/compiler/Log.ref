*$FROM Library
$EXTERN FOpen, FClose;

*$FROM LibraryEx
$EXTERN Map;

*$FROM DisplayName
$EXTERN DisplayName;

*$FROM Escape
$EXTERN EscapeString;

*$FROM Log-AST
$EXTERN Log-TextFromAST;


/**
  <Log-Init e.FileName?> == []
*/
$ENTRY Log-Init {
  /* no file name */ = /* do noting */;

  e.LogFileName
    = <FOpen "w" e.LogFileName> : s.FileHandle
    = <G_LogFile s.FileHandle> : /* empty */
    = <Log-PutLine 'Compilation starts'>;
}

$SWAP G_LogFile;


/**
  <Log-Final> == []
*/
$ENTRY Log-Final {
  /* empty */
    = <Log-PutLine 'Compilation ends'> : /* empty */
    = <G_LogFile>
    : {
        s.FileHandle = <FClose s.FileHandle>;

        /* empty */ = /* do nothing */;
      };
}

LogHandle {
  /* empty */
    = <G_LogFile>
    : {
        s.FileHandle = s.FileHandle <G_LogFile s.FileHandle>;

        /* empty */ = /* empty */;
      };
}


/**
  <Log-PutLine e.Line> == []
*/
$ENTRY Log-PutLine {
  e.Message
    = <LogHandle>
    : {
        /* none */ = /* do nothing */;

        s.FileHandle = <Putout s.FileHandle <Time> ': ' e.Message>;
      };
}


/**
  <Log-AST (e.Message) e.AST> == e.AST
*/
$ENTRY Log-AST {
  (e.Message) e.AST
    = <LogHandle>
    : {
        /* none */ = e.AST;

        s.FileHandle
          = <Log-PutLine 'AST of file ' e.Message ':'>
            <Map
              {
                (e.Line) = <Putout s.FileHandle '  ' e.Line>;
              }
              <Log-TextFromAST e.AST>
            >
            e.AST;
      };
}


/**
  <Log-HighLevelRASL (e.SrcName) e.RASL> == e.RASL
*/
$ENTRY Log-HighLevelRASL {
  (e.SrcName) e.RASL
    = <LogHandle>
    : {
        /* none */ = e.RASL;

        s.FileHandle
          = <Log-PutLine 'HighLevelRASL code of file ' e.SrcName ':'>
            <Map (&PutProgramElement s.FileHandle) e.RASL>
            e.RASL;
      };
}

PutProgramElement {
  s.FileHandle
  (Function s.ScopeClass (e.Name) e.Commands)
    = <PutRASLFunction s.FileHandle s.ScopeClass (e.Name) e.Commands>;

  s.FileHandle
  (CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code)
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' :: native {'>
      <PutCode s.FileHandle t.SrcPos e.Code>
      <Putout s.FileHandle '  }\n'>;

  s.FileHandle (CmdEnum s.ScopeClass e.Name)
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' :: enum'>;

  s.FileHandle (CmdSwap s.ScopeClass e.Name)
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' :: swap'>;

  s.FileHandle (CmdConditionFunc s.ScopeClass e.Name)
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' :: condition'>;

  s.FileHandle (CmdDeclaration s.ScopeClass e.Name)
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' :: extern'>;

  s.FileHandle (CmdDefineIdent e.Name)
    = <Putout s.FileHandle '$LABEL ' <DisplayName e.Name>>;

  s.FileHandle (CmdEmitNativeCode t.SrcPos e.Code)
    = <Putout s.FileHandle 'Native code:'>
      <PutCode s.FileHandle t.SrcPos e.Code>
      <Putout s.FileHandle>;
}

PutRASLFunction {
  s.FileHandle s.ScopeClass (e.Name) e.Commands
    = <Putout s.FileHandle <FnName s.ScopeClass e.Name> ' {'>
      <PutRASLCommands s.FileHandle ('    ') e.Commands>
      <Putout s.FileHandle '  }\n'>;
}

FnName {
  GN-Local e.Name = '  ' <DisplayName e.Name>;
  GN-Entry e.Name = '  $ENTRY ' <DisplayName e.Name>;
}

PutRASLCommands {
  s.FileHandle (e.Indent) e.Commands
    = <Map
        {
          (CmdSentence e.SubCommands)
            = <Putout s.FileHandle e.Indent '(CmdSentence'>
              <PutRASLCommands s.FileHandle (e.Indent '  ') e.SubCommands>
              <Putout s.FileHandle e.Indent ')  // CmdSentence'>;

          (CmdOpenELoop AlgLeft s.BracketNum s.VarNumber e.SubCommands)
            = <Putout
                 s.FileHandle e.Indent '(CmdOpenELoop'
                ' e.' <Symb s.VarNumber>
                ' <- [' <Symb s.BracketNum> ']'
              >
              <PutRASLCommands s.FileHandle (e.Indent '  ') e.SubCommands>
              <Putout s.FileHandle e.Indent ')  // CmdOpenELoop'>;

          (CmdComment e.Message)
            = <Putout s.FileHandle e.Indent '// ' e.Message>;

          /* TODO: pretty print */
          (s.Command e.Args)
            = <Putout s.FileHandle e.Indent '(' s.Command e.Args ')'>;
        }
        e.Commands
      >;
}

PutCode {
  s.FileHandle t.SrcPos e.Code
    = <Putout s.FileHandle '  ' <SrcPos t.SrcPos>>
      <Putout s.FileHandle '    ======================================'>
      <Map
        {
          (e.Line) = <Putout s.FileHandle '    | ' e.Line>;
        }
        e.Code
      >
      <Putout s.FileHandle '    ======================================'>;
}

SrcPos {
  s.LineNumber = '#line ' <Symb s.LineNumber>;

  (FileLine s.Line e.FileName)
    = '#line ' <Symb s.Line> ' "' <EscapeString e.FileName> '"';

  (RowCol s.Row s.Col) = '#line ' <Symb s.Row> ':' <Symb s.Col>;

  (FileRowCol (s.Row s.Col) e.FileName)
    = '#line ' <Symb s.Row> ':' <Symb s.Col>
      ' "' <EscapeString e.FileName> '"';
}
