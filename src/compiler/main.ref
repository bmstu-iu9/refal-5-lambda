$INCLUDE "LibraryEx";

*$FROM ParseCmdLine
$EXTERN ParseCommandLine;

*$FROM Config
$EXTERN Config-GetLogFile,
  Config-GetTargetFileName, Config-SetTargetFileName, Config-GetErrorFile,
  Config-GetCppCompiler, Config-GetGrammarCheck, Config-GetTargetSuffix,
  Config-GetSearchFolders, Config-GetTargetMode, Config-GetPrefix,
  Config-GetChmodXCommand, Config-GetReferences, Config-GetIncorporateds,
  Config-GetTempDir, Config-GetKeepRasls, Config-GetOverwriteExistRasls,
  Config-GetNoSources, Config-GetOptGlobal;

*$FROM Log
$EXTERN Log-Init, Log-Final;

*$FROM Error
$EXTERN PrintErr, EL-CheckEntryDuplicates;

*$FROM FindFile
$EXTERN FindFiles;

*$FROM Engine
$EXTERN CompileFile, GrammarCheck, GlobalASTStorage-Create,
  GlobalASTStorage-AddAST, GlobalASTStorage-Check, FrontEnd, BackEnd;

*$FROM Library
$EXTERN Open-Auto, Close, FSeek, FTell, WriteBytes, RenameFile, ReadBytes;

*$FROM Generator-RASL
$EXTERN PutBlockStart, PutBlockReference, PutBlockIncorporated,
  Rev_BlockTypeNumber;

*$FROM Platform
$EXTERN DirectorySeparator, IsDirectorySeparator;


$ENTRY Go {
  /* пусто */
    = <Main <ArgList>>
}

Main {
  (e.ProgName) ('@' e.Config)
    = <Main-SwValidCommandLine
        (e.Config) <ParseCommandLine <LoadFile e.Config>>
      >;

  (e.ProgName) e.Arguments
    = <Main-SwValidCommandLine
        CommandLine <ParseCommandLine e.Arguments>
      >;
}

Main-SwValidCommandLine {
  t.CommandLineSource Success t.Config /* файлов нет */
    , <Config-GetNoSources t.Config> : False
    = <ReportErrors
        t.CommandLineSource t.Config (1 'no source files in command line')
      >;

  t.CommandLineSource Success t.Config e.Files
    = <Log-Init <Config-GetLogFile t.Config>>
      <Main-SwFoundFiles
        t.CommandLineSource t.Config
        <LookupSourceFiles t.Config e.Files>
      >
      <Log-Final>;

  t.CommandLineSource Fails e.Errors
    = <ReportErrors t.CommandLineSource NoConfig e.Errors>;
}

$INLINE ReportErrors, ReportWarnings, ReportMessages;

ReportErrors {
  t.CommandLineSource t.Config e.Errors
    = <ReportMessages (&OnErrors 'Error') t.CommandLineSource t.Config e.Errors>;
}

OnErrors {
  t.Config
    = <CreateErrorFileMark t.Config>
      <Exit 1>;
}

ReportWarnings {
  t.CommandLineSource t.Config e.Warnings
    = <ReportMessages
        (&OnWarnings 'Warning') t.CommandLineSource t.Config e.Warnings
      >;
}

OnWarnings {
  t.Config = /* пусто */;
}

ReportMessages {
  (s.FnAfterMessages e.Banner) t.CommandLineSource t.Config e.Messages
    = t.CommandLineSource
    : {
        (e.ConfigFile) = e.ConfigFile ':';
        CommandLine = 'Command line argument ';
      }
    : e.Prefix
    = <Map
        {
          (s.Pos e.Message)
            = <PrintErr e.Prefix <Symb s.Pos> ': ' e.Banner ': ' e.Message>;
        }
        e.Messages
      >
      <s.FnAfterMessages t.Config>;
}

CreateErrorFileMark {
  /*
    Вызывается также при ошибке в командной строке, когда t.Config
    не удалось построить, поэтому NoConfig
  */
  NoConfig = /* ничего */;

  t.Config = <CreateErrorFileMark-SwFile <Config-GetErrorFile t.Config>>;
}

CreateErrorFileMark-SwFile {
  NoErrorFile = /* ничего */;
  ErrorFile e.FileName = <SaveFile (e.FileName)>;
}

Main-SwFoundFiles {
  t.CommandLineSource t.Config
  e.Files-B (s.Pos NotFound e.FileName) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        <Map
          {
            (s.Pos^ NotFound e.FileName^)
              = (s.Pos 'file ' e.FileName ' is not found');

            (s.Pos^ Source (s.Dialect e.Source) e.Output) = /* пусто */;
            (s.Pos^ Output e.Output) = /* пусто */;
            (s.Pos^ OutputWithNative (e.Output) e.NativeOutput) = /* пусто */;
          }
          (s.Pos NotFound e.FileName) e.Files-E
        >
      >;

  t.CommandLineSource t.Config e.Files
    = <CorrectTargetFileName t.CommandLineSource t.Config e.Files> : t.Config^
    = <Main-SwRenamedTargets
        t.CommandLineSource t.Config <RenameTargets t.Config e.Files>
      >;
}

CorrectTargetFileName {
  t.CommandLineSource t.Config t.FirstFile e.Files
    , <Config-GetTargetFileName t.Config>
    : {
        DefaultTargetFileName
          = t.FirstFile
          : {
              (s.Pos Source (s.Dialect e.Source) e.Output) = e.Output;
              (s.Pos Output e.Output) = e.Output;
              (s.Pos OutputWithNative (e.Output) e.NativeOutput) = e.Output
            }
          : e.Output

          = <GetBaseName e.Output> : e.LastPartName
          = <Lower e.LastPartName> : e.LowerBaseName '.rasl'    /* assert */
          = e.LastPartName : e.BaseName '.' s.R s.A s.S s.L
          = FromFirstFile e.BaseName;

        e.TargetFileName = FromCommandLine e.TargetFileName;
      }
    : s.Origin e.TargetFileName
    = <Config-SetTargetFileName t.Config s.Origin e.TargetFileName>
    : Success t.Config^
    = t.Config;

  t.CommandLineSource t.Config /* no files */
    , <Config-GetTargetFileName t.Config>
    : {
        DefaultTargetFileName
          = <ReportErrors
              t.CommandLineSource t.Config
              (1 'Option --no-sources require option -o (--target-file)')
            >;

        e.TargetFileName
          = <Config-SetTargetFileName t.Config FromCommandLine e.TargetFileName>
          : Success t.Config^
          = t.Config;
      };
}

Main-SwRenamedTargets {
  t.CommandLineSource
  t.Config
  e.Files-B (s.Pos Collision e.Collision) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        <Map
          {
            (s.Pos^ Collision (e.FirstTarget) e.SecondTarget)
              = (
                  s.Pos
                  'files ' e.FirstTarget ' and ' e.SecondTarget ' have same name'
                );

            (s.Pos^ Source (s.Dialect e.Source) e.Output) = /* пусто */;
            (s.Pos^ Output e.Output t.Native) = /* пусто */;
          }
          (s.Pos Collision e.Collision) e.Files-E
        >
      >;

  t.CommandLineSource
  t.Config
  e.Files
    = <Config-GetGrammarCheck t.Config> : s.GrammarCheck
    = s.GrammarCheck <CompileFiles t.Config s.GrammarCheck e.Files>
    : {
        s.GrammarCheck^ e.Outputs-B Fails e.Outputs-E
          = <CreateErrorFileMark t.Config>
            <Exit 1>;

        NormalRun e.Outputs
          = <Link t.CommandLineSource t.Config e.Outputs>;

        GrammarCheck e.Outputs
          = <Prout '** No syntax errors found **'>;
      };
}

/*----------------------------------------------------------------------------*/

CalcTargetFileName {
  s.TargetMode t.Config
    = <Config-GetTargetFileName t.Config>
    : {
        FromFirstFile e.BaseName
          = e.BaseName <Config-GetTargetSuffix t.Config s.TargetMode>;

        FromCommandLine e.TargetFileName = e.TargetFileName;
      };
}

LookupSourceFiles {
  t.Config e.Files
    = <Config-GetTargetMode t.Config> : s.TargetMode
    = <Config-GetSearchFolders t.Config> : e.SearchFolders
    = <Map
        {
          (s.Pos e.FileName)
            = <FindFiles s.TargetMode (e.SearchFolders) (e.FileName)>
            : {
                (s.ResultTag e.Info) = (s.Pos s.ResultTag e.Info);
              };
        }
        e.Files
      >;
}

RenameTargets {
  t.Config e.Files
    = <Config-GetTempDir t.Config> : e.TempDir
    = <ScanCollisions t.Config <Map (&GetFileName e.TempDir) e.Files>>;
}

GetFileName {
  e.TempDir (s.Pos Output e.FileName)
    = (
        (<GetBaseNameLowerCaseNoExt e.FileName>)
        (s.Pos Output e.FileName NoNative)
      );

  e.TempDir (s.Pos OutputWithNative (e.FileName) e.NativeFileName)
    = (
        (<GetBaseNameLowerCaseNoExt e.FileName>)
        (s.Pos Output e.FileName (e.NativeFileName))
      );

  e.TempDir (s.Pos Source (s.Dialect e.Source) e.Output)
    = <GetBaseName e.Output> : e.Output^
    = <GlueNames (e.TempDir) (e.Output)> : e.Output^
    = (
        (<GetBaseNameLowerCaseNoExt e.Output>)
        (s.Pos Source (s.Dialect e.Source) e.Output)
      );
}

ScanCollisions {
  t.Config
  ((e.BaseName) e.Locations1)
  e.Files-B ((e.BaseName) e.Locations2) e.Files-E
    = <ScanCollisions
        t.Config
        e.Files-B ((e.BaseName) e.Locations1 e.Locations2) e.Files-E
      >;

  t.Config
  ((e.BaseName) e.Locations) e.Files
    = <RenameLocations t.Config e.Locations> <ScanCollisions t.Config e.Files>;

  t.Config /* нет файлов */ = /* всё */;
}

RenameLocations {
  t.Config
  e.Locations-B (s.Pos1 Output e.FirstTarget t.FirstNative)
  e.Locations-M (s.Pos2 Output e.SecondTarget t.SecondNative)
  e.Locations-E
    = (s.Pos2 Collision (e.FirstTarget) e.SecondTarget);

  t.Config
  e.Locations-B (s.Pos Output e.Target t.Native) e.Locations-E
    = <RenameLocations-Aux
        t.Config 1 e.Locations-B (s.Pos Output e.Target t.Native) e.Locations-E
      >;

  t.Config e.Locations = <RenameLocations-Aux t.Config 0 e.Locations>;
}

RenameLocations-Aux {
  t.Config s.InitNumber e.Locations
    = <Config-GetOverwriteExistRasls t.Config> : s.Overwrite
    = <MapAccum
        {
          s.Num (s.Pos Source (s.Dialect e.Source) e.Output '.rasl')
            , s.Overwrite : False
            = <FindFreeName s.Num e.Output> : s.Num^ e.NewName
            = <Inc s.Num>
              (s.Pos Source (s.Dialect e.Source) e.NewName '.rasl');

          s.Num (s.Pos Source (s.Dialect e.Source) e.Output '.rasl')
            = <AddNumSuffix s.Num e.Output> : e.NewName
            = <Inc s.Num>
              (s.Pos Source (s.Dialect e.Source) e.NewName '.rasl');

          s.Num (s.Pos Output e.Output t.Native)
            = s.Num (s.Pos Output e.Output t.Native);
        }
        s.InitNumber e.Locations
      >
    : s.Num e.NewLocations
    = e.NewLocations;
}

$INLINE AddNumSuffix;

AddNumSuffix {
  0 e.Output = e.Output;
  s.Num e.Output = e.Output '@' <gen_e__ <Symb s.Num>>;
}

gen_e__ { e.arg = e.arg }

FindFreeName {
  s.Num e.Output
    = <AddNumSuffix s.Num e.Output> : e.NewName
    = <ExistFile e.NewName '.rasl'> <ExistFile e.NewName '.cpp'>
    : {
        e._B True e._E = <FindFreeName <Inc s.Num> e.Output>;

        False False = s.Num e.NewName;
      }
}

CompileFiles {
  t.Config NormalRun e.Files
    , <Config-GetOptGlobal t.Config> : OptGlobal
    = <CompileFiles-GlobalOpt t.Config e.Files>;

  t.Config NormalRun e.Files
    = <CompileFiles-NormalRun t.Config e.Files>;

  t.Config GrammarCheck e.Files
    = <CompileFiles-GrammarCheck t.Config e.Files>;
}

CompileFiles-GlobalOpt {
  t.Config e.Files
    = <CalcTargetFileName CompileOnly t.Config> : e.TargetName
    = <Open-Auto "w" e.TargetName '-locals.lst'> : s.LstFile
    = <MapAccum
        (&CompileFiles-GlobalOpt-EachFile t.Config s.LstFile)
        <GlobalASTStorage-Create>
        e.Files
      >
      <Close s.LstFile>
    : t.ASTStorage e.Outputs
    = <CompileFiles-GlobalOpt-CheckSucceededOutputs
        t.Config t.ASTStorage (e.TargetName) e.Outputs
      >
}

CompileFiles-GlobalOpt-EachFile {
  t.Config s.LstFile t.ASTStorage (s.Pos Output e.OutputName NoNative)
    = t.ASTStorage
      <Prout '+Linking ' e.OutputName>
      (RASL Given e.OutputName);

  t.Config s.LstFile t.ASTStorage (s.Pos Output e.OutputName (e.NativeName))
    = t.ASTStorage
      <Prout '+Linking (+ natives) ' e.OutputName>
      (RASL Given e.OutputName) (Native Given e.NativeName);

  t.Config s.LstFile t.ASTStorage
  (s.Pos Source (s.Dialect e.Source) e.OutputName '.rasl')
    = <Prout '*Parsing ' e.Source '...'>
      <FrontEnd t.Config s.Dialect e.Source>
    : {
        Success WithNative e.AST
          = <Prout '   ... with native, generating separately'>
            <BackEnd t.Config (e.Source) (e.OutputName) e.AST>
          : {
              Success e.OutputName^ NoNative
                = t.ASTStorage
                  (RASL Generated e.OutputName);

              Success e.OutputName^ (e.NativeOutputName)
                = t.ASTStorage
                  <Prout '   ... natives generated'>
                  (RASL Generated e.OutputName)
                  (Native Generated e.NativeOutputName);

              Fails = t.ASTStorage Fails;
            };

        Success NoNative e.AST
          = <GlobalASTStorage-AddAST t.ASTStorage e.AST> : t.ASTStorage^ s.Id
          = t.ASTStorage
            <Putout s.LstFile '~' <Symb s.Id> ' -- ' e.Source>
            /* генерация откладывается */;

        Fails = t.ASTStorage Fails;
      };
}

CompileFiles-GlobalOpt-CheckSucceededOutputs {
  t.Config t.ASTStorage (e.TargetName) e._ Fails e._ = Fails;

  t.Config t.ASTStorage (e.TargetName) e.Outputs
    = e.TargetName '.GLOBALS' : e.PseudoSourceName
    = <GlobalASTStorage-Check t.ASTStorage>
    : {
        Success /* пусто */ = e.Outputs;

        Success e.AST
          = <Prout '*Generating code...'>
            <BackEnd t.Config (e.PseudoSourceName) (e.TargetName) e.AST>
          : {
              Success e.OutputName NoNative
                = (RASL Generated e.OutputName)
                  e.Outputs;

              Success e.OutputName (e.NativeOutputName)
                = <Prout '   ... natives generated'>
                  (RASL Generated e.OutputName)
                  (Native Generated e.NativeOutputName)
                  e.Outputs;

              Fails = Fails;
            };

        Fails = Fails;
      };
}

CompileFiles-NormalRun {
  t.Config e.Files
    = <Map (&CompileFiles-NormalRun-EachFile t.Config) e.Files>;
}

CompileFiles-NormalRun-EachFile {
  t.Config (s.Pos Output e.OutputName NoNative)
    = <Prout '+Linking ' e.OutputName>
      (RASL Given e.OutputName);

  t.Config (s.Pos Output e.OutputName (e.NativeName))
    = <Prout '+Linking (+ natives) ' e.OutputName>
      (RASL Given e.OutputName) (Native Given e.NativeName);

  t.Config (s.Pos Source (s.Dialect e.Source) e.OutputName)
    = <Prout '*Compiling ' e.Source ':'> : /* пусто */
    = <CompileFile t.Config (s.Dialect e.Source) e.OutputName>
    : {
        Success e.OutputName^ NoNative = (RASL Generated e.OutputName);

        Success e.OutputName^ (e.NativeOutputName)
          = <Prout '   ... natives generated'>
            (RASL Generated e.OutputName)
            (Native Generated e.NativeOutputName);

        Fails = Fails;
      };
}

CompileFiles-GrammarCheck {
  t.Config e.Files
    = <Map
        {
          (s.Pos Output e.OutputName)
            = <Prout '+Skipping native file ' e.OutputName> (e.OutputName);

          (s.Pos Source (s.Dialect e.Source) e.OutputName)
            = <Prout '*Grammar checking ' e.Source ':'>
              <GrammarCheck t.Config s.Dialect e.Source>;
        }
        e.Files
      >;
}

Link {
  t.CommandLineSource t.Config e.Files
    = <Config-GetTargetMode t.Config>
    : {
        EXE
          = <Link-Exe
              t.CommandLineSource t.Config
              (<Config-GetCppCompiler t.Config EXE>)
              (<Config-GetPrefix t.Config>)
              e.Files
            >;

        LIB
          = <Link-Lib
              t.CommandLineSource t.Config
              (<Config-GetCppCompiler t.Config LIB>)
              (<Config-GetPrefix t.Config>)
              e.Files
            >;

        R = <Link-R t.CommandLineSource t.Config e.Files>;

        CompileOnly = /* ничего не делаем */;
      };
}

Link-Exe {
  t.CommandLineSource t.Config
  (NoCppCompiler) (/* нет префикса */) e.Files
    = <ReportErrors
        t.CommandLineSource t.Config
        (
          1
          'option -x (or --exe) require option '
          '--cpp-command-exe with nonempty value'
        )
      >;

  t.CommandLineSource t.Config
  ((e.CommandLine) e.Flags (e.CommandLineSuf)) (/* нет префикса */) e.Files
    = <Link-CppCompile
        t.Config ((e.CommandLine) e.Flags (e.CommandLineSuf)) e.Files
      >;

  t.CommandLineSource t.Config
  (e.AnyCommandLine) (e.Prefix)
  e.Files-B (Native e.FileName) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        (
          1
          'unexpected native file ' e.FileName ' while compilation runs '
          'with prefix'
        )
      >;

  t.CommandLineSource t.Config
  (e.AnyCommandLine) (e.Prefix) e.Files
    = <FindPrefix t.Config e.Prefix>
    : {
        Success e.PrefixFileName
          = <Link-WithPrefix (e.PrefixFileName) t.Config e.Files>;

        Fails e.ExpectedPrefixName
          = <ReportErrors
              t.CommandLineSource t.Config
              (1 'Can\'t find prefix ' e.ExpectedPrefixName)
            >;
      };
}

Link-Lib {
  t.CommandLineSource t.Config
  (NoCppCompiler) (/* нет префикса */)
  e.Files-B (Native e.FileName) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        (
          1
          'option -l (or --lib) with native code require option '
          '--cpp-command-lib with nonempty value'
        )
      >;

  t.CommandLineSource t.Config
  (NoCppCompiler) (/* нет префикса */) e.Files
    = <ReportWarnings
        t.CommandLineSource t.Config
        (
          1
          'prefix name or C++ command line is not found, '
          'create library without prefix'
        )
      >
      <Link-R t.CommandLineSource t.Config e.Files>;

  t.CommandLineSource t.Config
  ((e.CommandLine) e.Flags (e.CommandLineSuf)) (/* нет префикса */) e.Files
    = <Link-CppCompile
        t.Config ((e.CommandLine) e.Flags (e.CommandLineSuf)) e.Files
      >;

  t.CommandLineSource t.Config
  (e.AnyCommandLine) (e.Prefix)
  e.Files-B (Native e.FileName) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        (
          1
          'unexpected native file ' e.FileName ' while compilation runs '
          'with prefix'
        )
      >;

  t.CommandLineSource t.Config
  (e.AnyCommandLine) (e.Prefix) e.Files
    = <FindPrefix t.Config e.Prefix>
    : {
        Success e.PrefixFileName
          = <Link-WithPrefix (e.PrefixFileName) t.Config e.Files>;

        Fails e.ExpectedPrefixName
          = <ReportWarnings
              t.CommandLineSource t.Config
              (
                1
                'Can\'t find prefix ' e.ExpectedPrefixName ', create module '
                'without prefix (R module)'
              )
            >
            <Link-R t.CommandLineSource t.Config e.Files>;
      };
}

Link-R {
  t.CommandLineSource t.Config
  e.Files-B (Native e.FileName) e.Files-E
    = <ReportErrors
        t.CommandLineSource t.Config
        (1 'unexpected native file ' e.FileName ' while creating R module')
      >;

  t.CommandLineSource t.Config e.Files
    = <Link-Aux
        R
        {
          e.IntermediateName = /* ничего не делаем */;
        }
        t.Config e.Files
      >;
}

Link-WithPrefix {
  (e.PrefixFileName) t.Config e.Files
    = <Config-GetTargetMode t.Config> : s.TargetMode
    = <Link-Aux
        s.TargetMode
        {
          e.IntermediateName
            = <CopyPrefix (e.PrefixFileName) (e.IntermediateName)>
              <Chmod-X-Prefix t.Config e.IntermediateName>;
        }
        t.Config e.Files
      >;
}

Chmod-X-Prefix {
  t.Config e.IntermediateName
    = <Config-GetChmodXCommand t.Config>
    : {
        /* пусто */ = /* ничего не делаем */;

        e.ChmodXCommand
          = <System e.ChmodXCommand ' "' e.IntermediateName '"'>
          : e.RetCode
          = <CheckReturnCode t.Config (e.RetCode) 'chmod'>;
      };
}

CheckReturnCode {
  t.Config (0) e.Component = /* пусто */;

  t.Config (e.RetCode) e.Component
    = <LinkError
        t.Config
        e.Component ' finished with return code ' <Symb e.RetCode>
      >;
}

Link-CppCompile {
  t.Config ((e.CommandLine) e.Flags (e.CommandLineSuf)) e.Files
    = e.CommandLineSuf
    : {
        /* пусто */ = /* пусто */;
        e.CommandLineSuf^ = ' "' <Trim e.CommandLineSuf> '"';
      }
    : e.CommandLineSuf^
    = <Config-GetTargetMode t.Config> : s.TargetMode
    = <Link-Aux
        s.TargetMode
        {
          e.IntermediateName
            = <System
                e.CommandLine
                e.IntermediateName
                e.Flags
                <Map
                  {
                    (RASL s.Origin e.FileName) = /* пусто */;
                    (Native s.Origin e.FileName) = ' "' e.FileName '"';
                  }
                  e.Files
                >
                e.CommandLineSuf
              >
            : e.RetCode
            = <CheckReturnCode t.Config (e.RetCode) 'C++ compiler'>
              <CheckCppCompilationSuccessed t.Config e.IntermediateName>
              <EnsureAligned4096 e.IntermediateName>;
        }
        t.Config e.Files
      >;
}

Link-Aux {
  s.TargetMode s.FnCreatePrefix t.Config e.Files
    = <CalcTargetFileName s.TargetMode t.Config> : e.TargetFileName
    = e.TargetFileName '.partial' : e.IntermediateName
    = <Config-GetReferences t.Config> : e.References
    = <Config-GetIncorporateds t.Config> : e.IncorporatedLibNames
    = <PreLinkCleanupTemporary t.Config e.IntermediateName>
      <s.FnCreatePrefix e.IntermediateName>
      <AppendRASLToBinary (e.IntermediateName) e.Files>
      <AppendReferencesToBinary (e.IntermediateName) e.References>
      <AppendIncorporatedToBinary (e.IntermediateName) e.IncorporatedLibNames>
      <CheckEntryDuplicates t.Config e.IntermediateName>
      <PostLinkRename t.Config (e.IntermediateName) (e.TargetFileName)>
      <CleanupIntermediateFiles t.Config e.Files>
}

PreLinkCleanupTemporary {
  t.Config e.Temporary
    = <ExistFile e.Temporary>
    : {
        False = /* ничего не делаем */;

        True
          = <RemoveFile e.Temporary>
          : {
              True ()
                = <PrintErr 'Lost temporary file ' e.Temporary ' is removed'>;

              False (e.ErrorMessage)
                = <LinkError
                    t.Config
                    'Can\'t remove file ' e.Temporary ': ' e.ErrorMessage
                  >;
            };
      };
}

CheckCppCompilationSuccessed {
  t.Config e.TemporaryName
    = <ExistFile e.TemporaryName>
    : {
        False = <LinkError t.Config 'C++ compilation is failed'>;
        True = /* ничего не делаем */;
      };
}

EnsureAligned4096 {
  e.BinaryName
    = <Open-Auto ('ab') e.BinaryName> : s.FileHandle
    = <AlignToPage s.FileHandle>
      <Close s.FileHandle>;
}

AlignToPage {
  s.FileHandle
    = <FSeek s.FileHandle END '-' 0> : /* пусто */
    = <Mod <FTell s.FileHandle> 4096>
    : {
        0 = /* ничего не делаем */;

        s.Other =
          <WriteBytes s.FileHandle '@'>
          <AlignToPage s.FileHandle>;
      };
}

AppendRASLToBinary {
  (e.BinaryName) /* no files */
    = <AppendSimpleBlock &PutBlockStart (e.BinaryName) ()>;

  (e.BinaryName) e.Files
    = <Open-Auto ('ab') e.BinaryName> : s.FileHandle
    = <Map
        {
          (RASL s.Origin e.OutputName)
            = <WriteBytes s.FileHandle <LoadBytes e.OutputName>>;

          (Native s.Origin e.NativeName) = /* ничего не делаем */;
        }
        e.Files
      >
      <Close s.FileHandle>;
}

AppendReferencesToBinary {
  (e.BinaryName) e.References
    = <AppendSimpleBlock &PutBlockReference (e.BinaryName) e.References>;
}

AppendIncorporatedToBinary {
  (e.BinaryName) e.LibNames
    = <AppendSimpleBlock &PutBlockIncorporated (e.BinaryName) e.LibNames>;
}

AppendSimpleBlock {
  s.FnAdder (e.BinaryName) e.BlockItems
    = <Open-Auto ('ab') e.BinaryName> : s.FileHandle
    = <Map
        {
          (e.BlockItem) = <WriteBytes s.FileHandle <s.FnAdder e.BlockItem>>;
        }
        e.BlockItems
      >
      <Close s.FileHandle>;
}

PostLinkRename {
  t.Config (e.TemporaryName) (e.TargetFileName)
    = <ExistFile e.TargetFileName>
    : {
        True = <CheckedRemoveFile t.Config e.TargetFileName>;

        False = /* ничего не делаем */;
      }
    : /* пусто */
    = <RenameFile (e.TemporaryName) (e.TargetFileName)>
    : {
        True = <Prout '** Compilation succeeded **'>;

        False
          = <LinkError
              t.Config
              'Can\'t rename from ' e.TemporaryName ' to ' e.TargetFileName
            >;
      };
}

CheckedRemoveFile {
  t.Config e.FileName
    = <RemoveFile e.FileName>
    : {
        True () = /* ничего не делаем */;

        False (e.ErrorMessage)
          = <LinkError
              t.Config 'Can\'t remove file ' e.FileName ', ' e.ErrorMessage
            >;
       };
}

CleanupIntermediateFiles {
  t.Config e.Files, <Config-GetKeepRasls t.Config> : True
    = /* ничего не делаем */;

  t.Config e.Files
    = <Map
        {
          (s.FileType Given e.FileName) = /* пропускаем, он был до нас */;

          (s.FileType Generated e.FileName)
            = <CheckedRemoveFile t.Config e.FileName>;
        }
        e.Files
      >;
}

FindPrefix {
  t.Config e.Prefix
    = <Config-GetTargetMode t.Config>
    : {
        EXE = '.exe-prefix';
        LIB = '.lib-prefix';
      }
    : e.PrefixExt
    = <DoFindPrefix
        (e.Prefix e.PrefixExt) (Current) <Config-GetSearchFolders t.Config>
      >;
}

DoFindPrefix {
  (e.PrefixName) (e.NextFolder) e.Folders
    = <GlueNames (e.NextFolder) (e.PrefixName)> : e.PrefixFileName
    = <ExistFile e.PrefixFileName>
    : {
        True = Success e.PrefixFileName;
        False = <DoFindPrefix (e.PrefixName) e.Folders>;
      };

  /* В переменной среды REF5RSL префикс не ищем */
  (e.PrefixName) REF5RSL e.Folders
    = <DoFindPrefix (e.PrefixName) e.Folders>;

  (e.PrefixName) /* кончились папки */
    = Fails e.PrefixName;
}

GlueNames {
  (Current) (e.FileName) = e.FileName;
  (e.Folder) (e.FileName) = e.Folder <DirectorySeparator> e.FileName;
}

CopyPrefix {
  (e.OriginalPrefixName) (e.NewExeName)
    = <Open-Auto "rb" e.OriginalPrefixName> : s.SrcHandle
    = <Open-Auto "wb" e.NewExeName> : s.DestHandle
    = <DoCopyPrefix s.SrcHandle s.DestHandle>
}

DoCopyPrefix {
  s.SrcHandle s.DestHandle
    = <ReadBytes s.SrcHandle 4096>
    : {
        /* пусто */ = <Close s.SrcHandle> <Close s.DestHandle>;
        e.Bytes
          = <WriteBytes s.DestHandle e.Bytes>
            <DoCopyPrefix s.SrcHandle s.DestHandle>;
      }
}

$INLINE LinkError;

LinkError {
  t.Config e.Message
    = <PrintErr 'LINK ERROR: ' e.Message>
      <CreateErrorFileMark t.Config>
      <Exit 1>;
}

GetBaseNameLowerCaseNoExt {
  e.FileName '.rasl' = <Lower <GetBaseName e.FileName>>;
}

GetBaseName {
  e.Folder s.Sep e.RestOfPath
    , <IsDirectorySeparator s.Sep> : True
    = <GetBaseName e.RestOfPath>;

  e.BaseName = e.BaseName;
}

CheckEntryDuplicates {
  t.Config e.CheckedExecutable
    = <Open-Auto "rb" e.CheckedExecutable> : s.FileHandle
    = <LoadRASLPart s.FileHandle>
      <Close s.FileHandle>
    : {
        Success e.RASLBlocks = e.RASLBlocks;

        Fails
          = <CheckedRemoveFile t.Config e.CheckedExecutable>
            <LinkError t.Config 'Can\'t parse RASL in ' e.CheckedExecutable>;
      }
    : e.RASLBlocks
    = <CollectEntries e.RASLBlocks>
    : {
        Error UnitNameBeforeConstTable e.UnitName
          = <CheckedRemoveFile t.Config e.CheckedExecutable>
            <LinkError t.Config 'Unit name before CONST_TABLE: ' e.UnitName>;

        Error FunctionBeforeConstTable e.Function
          = <CheckedRemoveFile t.Config e.CheckedExecutable>
            <LinkError t.Config 'Function before CONST_TABLE: ' e.Function>;

        e.Entries = e.Entries;
      }
    : e.Entries
    = <EL-CheckEntryDuplicates e.Entries>
    : {
        EL-NoErrors = /* пусто */;

        EL-HasErrors
          = <CheckedRemoveFile t.Config e.CheckedExecutable>
            <LinkError t.Config 'Entry duplicates is found'>;
      };
}

LoadRASLPart {
  s.FileHandle
    = <ReadBytes s.FileHandle 4096>
    : {
        s.Type 8 0 0 0 s.R s.A s.S s.L s.C s.O s.D s.E e.RestOfRASL
          , <Rev_BlockTypeNumber s.Type> : Ok Start
          , <Ord 'RASLCODE'> : s.R s.A s.S s.L s.C s.O s.D s.E
          = <ParseRASLBlocks e.RestOfRASL <LoadRestOfFile s.FileHandle>>;

*       файл с размером, кратным 4096, но без сигнатуры,
*       является корректным префиксом
        /* пусто */ = Success /* пусто, ноль блоков */;

        e.Bytes
          = <Lenw e.Bytes>
          : {
              4096 e._ = <LoadRASLPart s.FileHandle>;
              s._ e._ = Fails;
          };
    };
}

LoadRestOfFile {
  s.FileHandle
    = <ReadBytes s.FileHandle 4096>
    : {
        /* пусто */ = /* пусто */;
        e.Bytes = e.Bytes <LoadRestOfFile s.FileHandle>;
    };
}

ParseRASLBlocks {
  e.Bytes
    = <DoParseRASLBlocks e.Bytes>
    : {
        e.Blocks Fails = Fails;
        e.Blocks = Success e.Blocks;
      };
}

DoParseRASLBlocks {
  s.Type s.S s.I s.Z s.E e.Tail
    , <Rev_BlockTypeNumber s.Type> : Ok s.Type^
    , <DecodeWord s.S s.I s.Z s.E> : s.Size
    = <First s.Size e.Tail EOF>
    : {
        (e.Block EOF) = Fails;
        (e.Block) e.Tail^ EOF = (s.Type e.Block) <DoParseRASLBlocks e.Tail>;
      };

  /* пусто */ = /* все блоки загружены */;

  e.Unparsed = Fails;
}

DecodeWord {
  s.Byte = s.Byte;
  s.Low e.High = <+ s.Low <* 256 <DecodeWord e.High>>>;
}

CollectEntries {
  (ConstTable e.T1) e.Unit (ConstTable e.T2) e.Blocks
    = <AttachPosToEntries e.Unit>
      <CollectEntries (ConstTable e.T2) e.Blocks>;

  (ConstTable e._) e.Unit = <AttachPosToEntries e.Unit>;

  (UnitName e.Name 0) e._
    = Error UnitNameBeforeConstTable <Chr e.Name>;

  (s.FuncType e.Name 0 e._) e.Blocks
    , <IsFunction s.FuncType> : True
    = Error FunctionBeforeConstTable <Chr e.Name>;

  t.OtherBlock e.Blocks = <CollectEntries e.Blocks>;

  /* нет блоков */ = /* нет и entry-функций */;
}

IsFunction {
  s.Type
     = <OneOf
         s.Type
         RefalFunction NativeFunction EmptyFunction Swap ConditionRasl
         ConditionNative Metatable
       >
}

AttachPosToEntries {
  e.Blocks-B (UnitName e.UnitName 0) e.Blocks-E
    = <Chr e.UnitName> : e.UnitName^
    = <Map
        {
           (s.FuncType e.FuncName 0 e._), <Chr e.FuncName> : '*' e.FuncName^
             = ((e.FuncName) (FileLine 1 e.UnitName));

           t.OtherBlock = /* пропускаем */
        }
        e.Blocks-B e.Blocks-E
      >;
}
