$INCLUDE "LibraryEx";

*$FROM Error
$EXTERN EL-Destroy, EL-Create, EL-AddErrorAt, EL-Concat;

*$FROM Config
$EXTERN
  Config-GetPrelude,
  Config-GetR5Mode,
  Config-GetSearchFolders,
  Config-GetDebugMode,
  Config-GetOptPattern,
  Config-GetOptResult,
  Config-GetConditionsMode,
  Config-GetGenMode,
  Config-GetOptTree;

*$FROM Checker
$EXTERN CheckProgram;

*$FROM R5-Lexer
$EXTERN R5-Scan;

*$FROM R5-Parser
$EXTERN R5-Parse;

*$FROM SR-Lexer
$EXTERN SR-Scan;

*$FROM SR-Parser
$EXTERN SR-ParseProgram;

*$FROM FindFile
$EXTERN FindInclude;

*$FROM Desugaring
$EXTERN Desugar;

*$FROM Log
$EXTERN Log-AST, Log-HighLevelRASL;

*$FROM OptTree
$EXTERN OptTree, OptTree-CleanupMarkup;

*$FROM HighLevelRASL
$EXTERN HighLevelRASL;

*$FROM LowLevelRASL
$EXTERN LowLevelRASL;

*$FROM Generator-RASL
$EXTERN GenProgram-RASL;

*$FROM Generator-Native
$EXTERN GenProgram-Native;


/**
  <CompileFile t.Config (s.Dialect e.SrcName) e.OutputName>
    == Success e.OutputName t.Native
    == Fails
  s.Dialect ::= R5 | SR
  t.Native ::= NoNative | (e.NativeOutputName)
*/
$ENTRY CompileFile {
  t.Config (s.Dialect e.SrcName) e.OutputName '.rasl'
    = <FrontEnd t.Config s.Dialect e.SrcName>
    : t.ErrorList e.AST
    = <EL-Destroy t.ErrorList>
    : {
        EL-NoErrors = <BackEnd t.Config (e.SrcName) (e.OutputName) e.AST>;

        EL-HasErrors = Fails;
      }
}

FrontEnd {
  t.Config s.Dialect e.SrcName
    = <LoadAST t.Config s.Dialect e.SrcName> : t.ErrorList e.FirstAST
    = <ResolveIncludes
        t.Config (/* bad includes */)
        (
          t.ErrorList s.Dialect ((e.SrcName) (e.SrcName))
          (/* scanned */) e.FirstAST
        )
      >
    : e.IncludeUnits
    = <GlueErrorListsAndASTs (e.SrcName) e.IncludeUnits> : t.ErrorList^ e.AST
    = <CheckProgram t.ErrorList e.AST> e.AST;
}

PreludeIncludeLine {
  t.Config
    = <Config-GetPrelude t.Config>
    : {
        /* пусто */ = /* пусто */;
        e.Prelude = (Include 1 e.Prelude);
      }
}

DisplayLexerWarnings {
  (e.FileName) e.Tokens
    = <Map
        {
          (TkWarning t.Pos e.Message)
            = <Prout e.FileName ':' <StrFromPos t.Pos> ':WARNING:' e.Message>;

          t.OtherToken = t.OtherToken;
        }
        e.Tokens
      >;
}

StrFromPos {
  NoPos = '1:1';
  s.Row = <Symb s.Row>;
  (RowCol s.Row s.Col) = <Symb s.Row> ':' <Symb s.Col>;
}

LoadAST {
  t.Config R5 e.SrcName
    = <Config-GetR5Mode t.Config> : s.Mode
    = <R5-Scan s.Mode e.SrcName> : e.Tokens
    = <DisplayLexerWarnings (e.SrcName) e.Tokens> : e.Tokens^
    = <R5-Parse <EL-Create e.SrcName> s.Mode e.Tokens> : t.ErrorList e.AST
    = t.ErrorList <PreludeIncludeLine t.Config> e.AST;

  t.Config SR e.SrcName
    = <SR-Scan e.SrcName> : e.Tokens
    = <DisplayLexerWarnings (e.SrcName) e.Tokens> : e.Tokens^
    = <SR-ParseProgram <EL-Create e.SrcName> e.Tokens>;

  t.Config RSL e.SrcName
    = e.SrcName : e.BaseName '.rsl'
    = e.BaseName '-decompiled.ref' : e.DecompiledName
    = (FileRowCol (1 1) e.DecompiledName) : t.ErrorPos
    = <System 'rsl-decompiler ' e.SrcName>
      <ExistFile e.DecompiledName>
    : {
        0 True
          = <LoadAST t.Config R5 e.DecompiledName> : t.ErrorList e.AST
          = <RemoveFile e.DecompiledName>
          : {
              True () = t.ErrorList e.AST;

              False (e.Message)
                = <EL-AddErrorAt
                    t.ErrorList t.ErrorPos
                    'Can\'t remove file ' e.DecompiledName ' (' e.Message ')'
                  >;
            };

        0 False
          = <EL-AddErrorAt
              <EL-Create> t.ErrorPos
              'RSL decompiler can\'t create ' e.DecompiledName
            >;

        e.RetCode s.Bool
          = <EL-AddErrorAt
              <EL-Create> t.ErrorPos
              'RSL decompiler failed with retcode ' <Symb e.RetCode>
            >;
      }
}

ResolveIncludes {
  t.Config (e.BadIncludes-B (e.IncludeName) e.BadIncludes-E)
  e.Units-B
  (
    t.ErrorList s.Dialect (e.Aliases)
    (e.ScannedAST) e.AST-B (Include t.SrcPos e.IncludeName) e.AST-E
  )
  e.Units-E
    = <ResolveIncludes
        t.Config
        (e.BadIncludes-B (e.IncludeName) e.BadIncludes-E)
        e.Units-B
        (
          <EL-AddErrorAt
            t.ErrorList t.SrcPos
            'Include file "' e.IncludeName '" is not found'
          >
          s.Dialect (e.Aliases)
          (e.ScannedAST e.AST-B (Include t.SrcPos e.IncludeName)) e.AST-E
        )
        e.Units-E
      >;

  t.Config (e.BadIncludes)
  e.Units-B
  (
    t.ErrorList s.Dialect (e.Aliases)
    (e.ScannedAST) e.AST-B (Include t.SrcPos e.IncludeName) e.AST-E
  )
  e.Units-E
    = <ResolveIncludes-CheckAlias
        t.Config (e.BadIncludes)
        (e.Units-B)
        (
          (
            t.ErrorList s.Dialect (e.Aliases)
            (e.ScannedAST e.AST-B (Include t.SrcPos e.IncludeName)) e.AST-E
          )
        )
        (e.Units-E)
        (e.IncludeName)
      >;

  t.Config (e.BadIncludes) e.Units
    = <Map
        {
          (
            t.ErrorList s.Dialect ((e.Name) (e.FullPath) e.Aliases)
            (e.ScannedAST) e.NotScannedAST
          )
            = (t.ErrorList (e.FullPath) e.ScannedAST e.NotScannedAST);
        }
        e.Units
      >;
}

ResolveIncludes-CheckAlias {
  t.Config (e.BadIncludes)
  e.Groups-B
  (
    e.Units-B
    (
      t.ErrorList s.Dialect (e.Aliases-B (e.IncludeName) e.Aliases-E)
      (e.ScannedAST) e.NotScannedAST
    )
    e.Units-E
  )
  e.Groups-E
  (e.IncludeName)
    = <ResolveIncludes
        t.Config (e.BadIncludes)
        <Map
          &UnBracket
          e.Groups-B
          (
            e.Units-B
            (
              t.ErrorList s.Dialect (e.Aliases-B (e.IncludeName) e.Aliases-E)
              (e.ScannedAST) e.NotScannedAST
            )
            e.Units-E
          )
          e.Groups-E
        >
      >;

   t.Config (e.BadIncludes)
   (e.Units-B) (t.CheckedUnit) (e.Units-E) (e.IncludeName)
     = <ResolveIncludes-CheckValid
         t.Config (e.BadIncludes)
         (e.Units-B) (t.CheckedUnit) (e.Units-E)
         (e.IncludeName)
         <FindInclude (<Config-GetSearchFolders t.Config>) e.IncludeName>
       >;
}

ResolveIncludes-CheckValid {
  t.Config (e.BadIncludes)
  (e.Units-B)
  (t.CheckedUnit)
  (e.Units-E)
  (e.IncludeName) Success s.Dialect e.FullPath
    = <ResolveIncludes-CheckEqualAST
        t.Config (e.BadIncludes)
        (e.Units-B) (t.CheckedUnit) (e.Units-E)
        s.Dialect (e.IncludeName) (e.FullPath)
        (<LoadAST t.Config s.Dialect e.FullPath>)
      >;

  t.Config (e.BadIncludes)
  (e.Units-B)
  (
    (
      t.ErrorList s.Dialect (e.Aliases)
      (e.ScannedAST (Include t.SrcPos e.IncludeName)) e.NotScannedAST
    )
  )
  (e.Units-E)
  (e.IncludeName) Fails
    = <ResolveIncludes
        t.Config (e.BadIncludes (e.IncludeName))
        e.Units-B
        (
          <EL-AddErrorAt
            t.ErrorList t.SrcPos
            'Include file "' e.IncludeName '" is not found'
          >
          s.Dialect (e.Aliases)
          (e.ScannedAST (Include t.SrcPos e.IncludeName)) e.NotScannedAST
        )
        e.Units-E
      >;
}

ResolveIncludes-CheckEqualAST {
  t.Config (e.BadIncludes)
  e.Groups-B
  (
    e.Units-B
    (t.ErrorList s.Dialect (e.Aliases) (e.ScannedAST) e.NotScannedAST)
    e.Units-E
  )
  e.Groups-E
  s.Dialect (e.IncludeName) (e.FullPath)
  (t.NewErrorList e.ScannedAST e.NotScannedAST)
    = <ResolveIncludes
        t.Config (e.BadIncludes)
        <Map
          &UnBracket
          e.Groups-B
          (
            e.Units-B
            (
              t.ErrorList s.Dialect (e.Aliases (e.IncludeName) (e.FullPath))
              (e.ScannedAST) e.NotScannedAST
            )
            e.Units-E
          )
          e.Groups-E
        >
      >;

  t.Config (e.BadIncludes)
  (e.Units-B) (t.CheckedUnit) (e.Units-E)
  s.Dialect (e.IncludeName) (e.FullPath) (t.ErrorList e.AST)
    = <ResolveIncludes
        t.Config (e.BadIncludes)
        e.Units-B
        (
          t.ErrorList s.Dialect ((e.IncludeName) (e.FullPath))
          (/* scanned */) e.AST
        )
        t.CheckedUnit
        e.Units-E
      >;
}

GlueErrorListsAndASTs {
  (e.SrcName) e.Units
    = <MapReduce
        {
          t.CommonErrorList (t.ErrorList (e.UnitName) e.AST)
            = <EL-Concat t.CommonErrorList t.ErrorList>
              <PatchASTSrcPos (e.UnitName) e.AST>;
        }
        <EL-Create e.SrcName>
        e.Units
      >;
}

PatchASTSrcPos {
  (e.FileName) e.AST
    = {
        NoPos = NoPos;
        s.LnNum = (FileLine s.LnNum e.FileName);
        (RowCol s.Row s.Col) = (FileRowCol (s.Row s.Col) e.FileName);
      }
    : s.FnPatch
    = <Map (&PatchASTSrcPos-Item s.FnPatch) e.AST>;
}

PatchASTSrcPos-Item {
  s.FnPatch (Function t.SrcPos s.ScopeClass (e.Name) e.Body)
    = (Function
        <s.FnPatch t.SrcPos>
        s.ScopeClass (e.Name)
        <PatchASTSrcPos-Body s.FnPatch e.Body>
      );

  s.FnPatch (Ident t.SrcPos e.Name) = (Ident <s.FnPatch t.SrcPos> e.Name);

  s.FnPatch (NativeBlock t.SrcPos e.Name)
    = (NativeBlock <s.FnPatch t.SrcPos> e.Name);

  s.FnPatch (Include t.SrcPos e.IncludeName) = /* пропускаем */;

  s.FnPatch (s.Label t.SrcPos s.ScopeClass e.Name)
    , <OneOf
        s.Label Enum Swap Declaration Entry Inline Drive
      > : True
    = (s.Label <s.FnPatch t.SrcPos> s.ScopeClass e.Name);

  s.FnPatch (Spec t.SrcPos (e.Name) e.Pattern)
    = (
        Spec <s.FnPatch t.SrcPos> (e.Name)
        <PatchASTSrcPos-Expr s.FnPatch e.Pattern>
      );
}

PatchASTSrcPos-Body {
  s.FnPatch Sentences e.Sentences
    = Sentences <Map (&PatchASTSrcPos-Sentence s.FnPatch) e.Sentences>;

  s.FnPatch NativeBody t.SrcPos e.Code
    = NativeBody <s.FnPatch t.SrcPos> e.Code;
}

PatchASTSrcPos-Sentence {
  s.FnPatch ((e.Pattern) e.ConditionsAndAssigns (e.Result) (e.Blocks))
    = { e.Expr = <PatchASTSrcPos-Expr s.FnPatch e.Expr>; } : s.FnPatchExpr
    = (
        (<s.FnPatchExpr e.Pattern>)
        <PatchASTSrcPos-Chain s.FnPatch s.FnPatchExpr e.ConditionsAndAssigns>
        (<s.FnPatchExpr e.Result>)
        (<PatchASTSrcPos-Blocks s.FnPatch e.Blocks>)
      );
}

PatchASTSrcPos-Chain {
  s.FnPatch s.FnPatchExpr e.ConditionsAndAssigns
    = <Map
        {
          (s.ChainType (e.Result^) (e.Blocks) (e.Pattern^))
            = (s.ChainType
                (<s.FnPatchExpr e.Result>)
                (<PatchASTSrcPos-Blocks s.FnPatch e.Blocks>)
                (<s.FnPatchExpr e.Pattern>)
              );
        }
        e.ConditionsAndAssigns
      >;
}

PatchASTSrcPos-Blocks {
  s.FnPatch e.Blocks
    = <Map
        {
          (e.Body) = (<PatchASTSrcPos-Body s.FnPatch e.Body>);
        }
        e.Blocks
      >;
}

PatchASTSrcPos-Expr {
  s.FnPatch e.Expression
    = <Map
        {
          (TkVariable t.SrcPos s.Mode e.Index)
            = (TkVariable <s.FnPatch t.SrcPos> s.Mode e.Index);

          (TkNewVariable t.SrcPos s.Mode e.Index)
            = (TkNewVariable <s.FnPatch t.SrcPos> s.Mode e.Index);

          (Symbol Name t.SrcPos e.Name)
            = (Symbol Name <s.FnPatch t.SrcPos> e.Name);

          (Brackets e.InBrackets)
            = (Brackets <PatchASTSrcPos-Expr s.FnPatch e.InBrackets>);

          (ADT-Brackets t.SrcPos (e.Name) e.InBrackets)
            = (ADT-Brackets
                <s.FnPatch t.SrcPos>
                (e.Name)
                <PatchASTSrcPos-Expr s.FnPatch e.InBrackets>
              );

          (CallBrackets e.InBrackets)
            = (CallBrackets <PatchASTSrcPos-Expr s.FnPatch e.InBrackets>);

          (Closure e.Sentences)
            = (Closure <PatchASTSrcPos-Body s.FnPatch e.Sentences>);

          t.OtherTerm = t.OtherTerm;
        }
        e.Expression
      >;
}

BackEnd {
  t.Config (e.SrcName) (e.OutputName) e.AST
    = <Config-GetDebugMode t.Config> : s.MarkupContext s.DebugInfo
    = <Config-GetOptPattern t.Config> : s.OptPattern
    = <Config-GetOptResult t.Config> : s.OptResult
    = <Config-GetConditionsMode t.Config> : s.ConditionsMode
    = <Config-GetGenMode t.Config> : s.GenMode
    = <Desugar s.MarkupContext s.ConditionsMode e.AST> : e.AST^
    = <Log-AST (e.SrcName ' (after desugar)') e.AST> : e.AST^
    = <Config-GetOptTree t.Config>
    : {
        OptTree = <OptTree t.Config e.AST>;
        NoOpt = <OptTree-CleanupMarkup e.AST>;
      }
    : e.AST^
    = <Log-AST (e.SrcName ' (after tree optimization)') e.AST> : e.AST^
    = <HighLevelRASL s.OptPattern s.OptResult e.AST> : e.HighLevelRASL
    = <Log-HighLevelRASL (e.SrcName) e.HighLevelRASL> : e.HighLevelRASL^
    = <LowLevelRASL s.GenMode s.DebugInfo (e.SrcName) e.HighLevelRASL>
    : {
        t.RASLModule =
          <BackEnd-RASLModule t.RASLModule e.OutputName '.rasl'>
          NoNative;

        t.RASLModule t.NativeModule =
          <BackEnd-RASLModule t.RASLModule e.OutputName '.rasl'>
          <BackEnd-NativeModule
            t.NativeModule (e.SrcName) e.OutputName '.cpp'
          >;
      }
}

BackEnd-RASLModule {
  t.RASLModule e.OutputName
    = <GenProgram-RASL t.RASLModule>
    : {
        e.ByteStream =
          <SaveBytes (e.OutputName) e.ByteStream>
          Success e.OutputName;
      }
}

BackEnd-NativeModule {
  t.NativeModule (e.SrcName) e.OutputName =
    <SaveFile
      (e.OutputName)
      <GenProgram-Native (e.SrcName) (e.OutputName) t.NativeModule>
    >
    (e.OutputName);
}

/**
  <GrammarCheck t.Config s.Dialect e.SrcName>
    == Success
    == Fails
*/
$ENTRY GrammarCheck {
  t.Config s.Dialect e.SrcName
    = <LoadAST t.Config s.Dialect e.SrcName> : t.ErrorList e.AST
    = <EL-Destroy t.ErrorList>
    : {
        EL-NoErrors = Success;
        EL-HasErrors = Fails;
      };
}
