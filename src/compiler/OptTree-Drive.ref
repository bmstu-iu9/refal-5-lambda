$INCLUDE "LibraryEx";


*$FROM TreeUtils
$EXTERN ExtractVariables, HashName, IsLExpr;

*$FROM Log
$EXTERN Log-PutLine;

*$FROM DisplayName
$EXTERN DisplayName;

*$FROM OptTree-Drive-Expr
$EXTERN OptTree-Drive-Expr, ApplyContractions;


/*
  В дерево добавляется узел (DriveInfo e.DriveInfo).

  e.DriveInfo ::= (e.OptFuncNames (e.OptFunctions))
  e.OptFuncNames ::= e.HashSet
  e.HashSet ::= s.BucketSize (e.Bucket)*
  e.Bucket ::= (s.Hash (s.Label e.Name)*)
  e.OptFunctions ::= t.OptFunction*

  t.OptFunction ::= (s.Label (e.Name) e.OptBody)
  e.OptBody ::= Sentences e.Sentences
*/


/**
  <OptTree-Drive-Prepare (s.OptDrive s.OptIntrinsic) e.AST> == e.AST

  s.OptDrive ::= OptDrive | OptInline | NoOpt
  s.OptIntrinsic ::= OptIntrinsic | NoOpt
*/
$ENTRY OptTree-Drive-Prepare {
  (NoOpt NoOpt) e.AST = e.AST;

  (s.OptDrive s.OptIntrinsic)
  e.AST-B (DriveInfo e.DriveInfo) e.AST-E
    = <UpdateDriveInfo s.OptDrive s.OptIntrinsic (e.DriveInfo) e.AST-B e.AST-E>;

  (s.OptDrive s.OptIntrinsic) e.AST
    = (<BUCKET-SIZE> /* no names */) /* no info */ : e.EmptyInfo
    = <UpdateDriveInfo s.OptDrive s.OptIntrinsic (e.EmptyInfo) e.AST>;
}

/* Функция генерирует имена остаточных прогоняемых функций */

GenRemainderFuncNames {
  s.Label e.Name s.End s.End = /* пусто — пустую функцию прогнать нельзя */;

  s.Label e.Name s.Cur s.End
    = (s.Label <AddStarSuffix e.Name s.Cur>)
      <GenRemainderFuncNames s.Label e.Name <+ 1 s.Cur> s.End>;
}

AddStarSuffix {
  e.Name 0 = e.Name;
  e.Name SUF e.Suffix s.Count = e.Name SUF e.Suffix '*' s.Count;
  e.Name s.Count = e.Name SUF '*' s.Count;
}

BaseName {
  e.Name SUF '*' s.Count = e.Name s.Count;
  e.Name '*' s.Count = e.Name s.Count;
  e.Name /* нет суффикса */ = e.Name 0;
}

UpdateDriveInfo {
  s.OptDrive s.OptIntrinsic ((e.KnownNames) e.KnownFunctions) e.AST
    = <ExtractLabels Drive e.AST> : (e.Drives) e.AST^
    = <ExtractLabels Inline e.AST> : (e.Inlines) e.AST^
    = <ExtractLabels Intrinsic e.AST> : (e.Intrinsics) e.AST^
    = <ExtractMetatableNames e.AST> : (e.Metatables) e.AST^

    = <SetNames-Reject (e.Drives) (e.Intrinsics)> : e.Drives^
    = <SetNames-Reject (e.Inlines) (e.Intrinsics)> : e.Inlines^

    = <HashSet-AsChain e.KnownNames> : e.KnownNames^

    = <SetNames-Reject (e.Drives) (e.KnownNames)> : e.Drives^
    = <SetNames-Reject (e.Inlines) (e.KnownNames)> : e.Inlines^
    = <SetNames-Reject (e.Intrinsics) (e.KnownNames)> : e.Intrinsics^
    = <SetNames-Reject (e.Metatables) (e.KnownNames)> : e.Metatables^

    = <Map
        {
          (s.Label e.Name)
            = '$' <Upper <Explode s.Label>> : e.Label
            = <Log-PutLine 'New ' e.Label ' function: ' <DisplayName e.Name>>;
        }
        e.Drives e.Inlines e.Intrinsics e.Metatables
      >
    : /* пусто */

    = s.OptDrive
    : {
        NoOpt = /* пусто */;
        OptInline = <Map { (Drive e.Name) = (Inline e.Name) } e.Drives>;
        OptDrive = e.Drives;
      }
    : e.Drives^

    = s.OptDrive
    : {
        NoOpt = /* пусто */;
        OptInline = e.Inlines;
        OptDrive = e.Inlines;
      }
    : e.Inlines^

    = <ExtractBaseNames e.Drives e.Inlines e.Metatables> : e.ExtractedNames
    = <ExtractExtractableFunctions (e.ExtractedNames) e.AST>
    : (e.Extracted) e.AST^

    = <ExtractFuncLengths e.AST> : (e.Lengths) e.AST^
    = <MultiplyRemainderNames (e.Lengths) e.Drives> : e.Drives^
    = <MultiplyRemainderNames (e.Lengths) e.Inlines> : e.Inlines^

    /*
      Функция Func может быть помечена как $INLINE, рассахариватель условий
      извлёк Func*k, авторазметка пометила её как $DRIVE.
      Пометка $INLINE имеет приоритет.
    */
    = <SetNames-Reject (e.Drives) (e.Inlines)> : e.Drives^

    = e.Drives e.Inlines e.Metatables : e.OptNames

    = <FormatOptFunctions (e.OptNames) e.Extracted> : e.OptFunctions

    = s.OptIntrinsic
    : {
        NoOpt = /* пусто */;
        OptIntrinsic = e.Intrinsics;
      }
    : e.Intrinsics^

    = <Map
        {
          (Intrinsic e.Name) = (Intrinsic (e.Name) Intrinsic e.Name);
        }
        e.Intrinsics
      >
    : e.OptIntrinsicFuncs

    = <HashSet e.KnownNames e.OptNames e.Intrinsics> : e.HashSetNames

    = (DriveInfo
        (e.HashSetNames)
        e.KnownFunctions e.OptFunctions e.OptIntrinsicFuncs
      )
      e.AST;
}

ExtractLabels {
  s.Label e.AST
    = <DoExtractLabels s.Label (/* метки */) (/* просканировано */) e.AST>;
}

DoExtractLabels {
  s.Label (e.FoundLabels) (e.Scanned) e.AST-B (s.Label e.Name) e.AST-E
    = <DoExtractLabels
        s.Label (e.FoundLabels (s.Label e.Name)) (e.Scanned e.AST-B) e.AST-E
      >;

  s.Label (e.FoundLabels) (e.Scanned) e.AST = (e.FoundLabels) e.Scanned e.AST;
}

ExtractMetatableNames {
  e.AST
    = <MapAccum
        {
          (e.Metatables) (Function s.ScopeClass (e.Name) Metatable e.Body)
            = (e.Metatables (Metatable e.Name))
              (Function s.ScopeClass (e.Name) Metatable e.Body);

          (e.Metatables) t.Other = (e.Metatables) t.Other;
        }
        (/* metatables */) e.AST
      >;
}

SetNames-Reject {
  (e.Minuent-B (s._M e.Name) e.Minuent-E) (e.Subtr-B (s.Label e.Name) e.Subtr-E)
    = e.Minuent-B
      <SetNames-Reject (e.Minuent-E) (e.Subtr-B (s.Label e.Name) e.Subtr-E)>;

  (e.Minuent) (e.Subtrahend) = e.Minuent;
}

ExtractBaseNames {
  e.OptNames
    = <Map
        {
          (s.Label e.Name)
            = <BaseName e.Name> : e.BaseName s.Num
            = (s.Label e.BaseName);
        }
        e.OptNames
      >
    : e.BaseOptNames
    = <Unique e.BaseOptNames>;
}

ExtractExtractableFunctions {
  (e.Names) e.AST
    = <MapAccum
        {
          ((e.Names-B (s._ e.Name) e.Names-E) e.Extracted)
          (Function s.ScopeClass (e.Name) e.Body)
            = (
                (e.Names-B e.Names-E)
                e.Extracted (Function s.ScopeClass (e.Name) e.Body)
              )
              (Function s.ScopeClass (e.Name) e.Body);

          ((e.Names^) e.Extracted) t.Other = ((e.Names) e.Extracted) t.Other;
        }
        ((e.Names) /* extracted */) e.AST
      >
    : ((/* должно быть пусто */) e.Extracted) e.AST^
    = (e.Extracted) e.AST;
}

ExtractFuncLengths {
  e.AST
    = <MapAccum
        {
          (e.Lengths) (Function s.ScopeClass (e.Name) Sentences e.Sentences)
            = <Lenw e.Sentences> : s.SentencesCount e.Sentences^
            = (e.Lengths (e.Name s.SentencesCount))
              (Function s.ScopeClass (e.Name) Sentences e.Sentences);

          (e.Lengths) t.Other = (e.Lengths) t.Other;
        }
        (/* длины */) e.AST
      >;
}

MultiplyRemainderNames {
  (e.Lengths) e.FuncNames
    = <MapAccum
        {
          (e.Lengths^) (s.Label e.OptName)
            = <BaseName e.OptName> : e.BaseName s.StartNum
            = e.Lengths : e.Lengths-B (e.BaseName s.SentencesCount) e.Lengths-E
            = (e.Lengths-B (e.BaseName s.SentencesCount) e.Lengths-E)
              <GenRemainderFuncNames
                s.Label e.BaseName s.StartNum s.SentencesCount
              >;
        }
        (e.Lengths) e.FuncNames
      >
    : (e._) e.RemainderNames
    = e.RemainderNames;
}

FormatOptFunctions {
  (e.OptNames) e.Functions
    = <MapAccum
        {
          (e.Names-B (Metatable e.Name) e.Names-E)
          (Function s.ScopeClass (e.Name) Metatable e.Body)
            = (e.Names-B e.Names-E)
              (Metatable (e.Name) Metatable e.Body);

          (e.Names-B (s.Label e.Name) e.Names-E)
          (Function s.ScopeClass (e.Name) Sentences e.Sentences)
            = (e.Names-B e.Names-E)
              (s.Label (e.Name) Sentences e.Sentences);

          (e.Names) (Function s._ (e._) Sentences /* пусто */)
            = (e.Names) /* функции без предложений пропускаем */;
        }
        (<ExtractBaseNames e.OptNames>) e.Functions
      >
    : (e._) e.OptFunctions
    = e.OptFunctions;
}

HashSet {
  e.Names
    = <BUCKET-SIZE>
      <DoHashSet
        <Map
          {
            (s.Label e.Name)
              = (<HashName e.Name <BUCKET-SIZE>> s.Label e.Name)
          }
          e.Names
        >
      >;
}

BUCKET-SIZE { = 101 }

DoHashSet {
  (s.Hash e.Name) e.Names
    = <DoHashSet-DoFillBucket e.Names (s.Hash (e.Name))> : e.Names^ (e.Bucket)
    = (e.Bucket) <DoHashSet e.Names>;

  /* нет имён */ = /* нет корзин */;
}

DoHashSet-DoFillBucket {
  e.Names-OtherHash (s.Hash e.Name) e.UnscannedNames (s.Hash e.Bucket)
    = e.Names-OtherHash
      <DoHashSet-DoFillBucket e.UnscannedNames (s.Hash e.Bucket (e.Name))>;

  e.Names-OtherHash (s.Hash e.Bucket) = e.Names-OtherHash (s.Hash e.Bucket);
}

HashSet-AsChain {
  s.BucketSize e.HashSet = <Map { (s.Hash e.Names) = e.Names } e.HashSet>
}


/**
  <OptTree-Drive e.AST> == e.AST^
*/
$ENTRY OptTree-Drive {
  e.AST-B (DriveInfo e.DriveInfo) e.AST-E
    = <DriveInlineOptimizerTick (e.DriveInfo) e.AST-B e.AST-E>
}

/* Осуществляет прогонку или оптимизацию в зависимости от ключа */

DriveInlineOptimizerTick {
  ((e.OptNames) e.OptInfo) e.AST
    = ((e.OptNames) <WithMetatables e.OptInfo>) : t.OptInfo
    = <MapAccum
        {
          t.OptInfo^ (Function s.ScopeClass (e.Name) Sentences e.Sentences)
            = <OptFunction
                (Function s.ScopeClass (e.Name) Sentences e.Sentences)
                t.OptInfo
              >;

          t.OptInfo^ t.Other = t.OptInfo t.Other
        }
        t.OptInfo
        e.AST
      >
    : ((e.OptNames^) t.Metatables e.OptInfo^) e.AST^

    = <MapAccum
        {
          (e.News-B (Function s.ScopeClass (e.Name) e.Body) e.News-E)
          (NewFunction s.ScopeClass (e.Name) e._)
            = (e.News-B (Function s.ScopeClass (e.Name) e.Body) e.News-E)
              /* пропускаем */;

          (e.NewFunctions) (NewFunction s.ScopeClass (e.Name) e.Body)
            = (e.NewFunctions (Function s.ScopeClass (e.Name) e.Body));

          (e.NewFunctions) t.Other = (e.NewFunctions) t.Other;
        }
        (/* new functions */) e.AST
      >
    : (e.NewFunctions) e.AST^

    = <MapAccum
        {
          (e.News-B (Function s.ScopeClass (e.Name) e._) e.News-E)
          (Function s.ScopeClass (e.Name) e.Body)
            = (e.News-B e.News-E)
              (Function s.ScopeClass (e.Name) e.Body);

          (e.News-B (Function s.ScopeClass (e.Name) e._) e.News-E)
          (ColdFunction s.ColdBy s.ScopeClass (e.Name) e.Body)
            = (e.News-B e.News-E)
              (ColdFunction s.ColdBy s.ScopeClass (e.Name) e.Body);

          (e.NewFunctions^) t.Other = (e.NewFunctions) t.Other;
        }
        (e.NewFunctions) e.AST
      >
    : (e.NewFunctions^) e.AST^

    = (DriveInfo (e.OptNames) e.OptInfo) e.AST e.NewFunctions;
}

/*
  Осуществляет попытку прогонки или встраивания в функции
  <OptFunction t.Function t.DriveInfo>
    == t.DriveInfo t.OptimizedFunction t.Function*

  Возвращает модифицированную функцию
  и порожденные из прогонки или встраивания
*/

OptFunction {
  (Function s.ScopeClass (e.Name) Sentences e.Sentences) t.OptInfo
    = <Reduce
        {
          (t.OptInfo^ (e.SntAcc) (e.FunAcc)) t.Sentence
            = <OptSentence t.Sentence t.OptInfo>
            : t.OptInfo^ (e.NewFunctions) e.NewSentences
            = (t.OptInfo (e.SntAcc e.NewSentences) (e.FunAcc e.NewFunctions));
        }
        (t.OptInfo (/*sentences*/) (/*functions*/))
        e.Sentences
      >
    : {
        (t.OptInfo^ (e.Sentences /* SAME */) (/* пусто */))
          = t.OptInfo
            (ColdFunction DRIVE
              s.ScopeClass (e.Name) Sentences <CleanupCold-Body e.Sentences>
            );

        (t.OptInfo^ (e.Sentences^) (e.NewFunctions))
          = t.OptInfo
            (Function s.ScopeClass (e.Name) Sentences e.Sentences)
            e.NewFunctions;
      }
}

/*
   <OptSentence t.Sentence t.OptInfo>
   == t.OptInfo (t.Func?) t.Sentence*
*/

OptSentence {
  /* Оптимизируем только предложения вида Pat = Res */
  ((e.Left) (e.Right)) t.OptInfo

    = <ExtractVariables ((e.Left) (e.Right))> : e.SentenceVars

    /* Если левая часть не L-выражение, сужения запрещаем */
    = <IsLExpr e.Left>
    : {
        True = /* пусто */;
        False = e.SentenceVars;
      }
    : e.WholeVars

     = <OptTree-Drive-Expr (e.SentenceVars) (e.WholeVars) t.OptInfo e.Right>
     : t.OptInfo^ e.Branches (e.NewFunctions)

     = t.OptInfo (e.NewFunctions)
       <Map
         {
           (e.Contractions (e.DrivenRight))
             = ((<ApplyContractions e.Left (e.Contractions)>) (e.DrivenRight));
         }
         e.Branches
       >;

  t.Sentence t.OptInfo = t.OptInfo () t.Sentence
}

WithMetatables {
  e.OptFuncs
    = <MapAccum
        {
          (Metatables e.Result) (Metatable e.Body)
             = (Metatables e.Result (e.Body)) (Metatable e.Body);

          (Metatables e.Result) t.Other = (Metatables e.Result) t.Other;
        }
        (Metatables) e.OptFuncs
      >
}

/**
  <OptTree-Drive-CleanupColdCalls e.AST> == e.AST^
*/
$ENTRY OptTree-Drive-CleanupColdCalls {
  e.AST
    = <Map
        {
          (Function s.ScopeClass (e.Name) Sentences e.Body)
            = (Function
                s.ScopeClass (e.Name) Sentences <CleanupCold-Body e.Body>
              );

          t.OtherItem = t.OtherItem;
        }
        e.AST
      >;
}

CleanupCold-Body {
  e.Sentences = <Map &CleanupCold-Sentence e.Sentences>;
}

CleanupCold-Sentence {
  ((e.Pattern) e.Conditions (e.Result))
    = (
        (e.Pattern)
        <Map
          {
            (Condition (e.Name) (e.Result^) (e.Pattern^))
              = (Condition
                  (e.Name) (<CleanupCold-Result e.Result>) (e.Pattern)
                );
          }
          e.Conditions
        >
        (<CleanupCold-Result e.Result>)
      );
}

CleanupCold-Result {
  e.Result = <Map &CleanupCold-Term e.Result>;
}

CleanupCold-Term {
  (Brackets e.Expr) = (Brackets <CleanupCold-Result e.Expr>);

  (ADT-Brackets (e.Name) e.Expr)
    = (ADT-Brackets (e.Name) <CleanupCold-Result e.Expr>);

  (CallBrackets e.Expr) = (CallBrackets <CleanupCold-Result e.Expr>);
  (ColdCallBrackets e.Expr) = (CallBrackets <CleanupCold-Result e.Expr>);

  t.OtherTerm = t.OtherTerm;
}
