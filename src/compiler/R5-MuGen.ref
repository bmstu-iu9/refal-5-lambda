$ENTRY R5-AddMu {
  e.AST
    , <Builtins> : e.Builtins
    , <DoAddMu-Names e.Builtins> : e.MuBuiltins
    , <DoAddMu-AST () e.AST> : (e.MuAST) e.AST
    = (Function NoPos GN-Local ('Mu') Sentences e.MuBuiltins e.MuAST) e.AST;
}

Builtins {
  = ('+') ('-') ('*') ('/') ('%') ('?') <DoBuiltins <ListOfBuiltin>>;
}

DoBuiltins {
  (s.Number s.Name s.Type) e.Builtins
    = (<Explode s.Name>) <DoBuiltins e.Builtins>;

  /* пусто */ = /* пусто */;
}

DoAddMu-Names {
  /* Служебное имя, пропускаем */
  (e.NextName '$$') e.Tail = <DoAddMu-Names e.Tail>;

  /* Пустое имя, пропускаем */
  () e.Tail = <DoAddMu-Names e.Tail>;

  (e.NextName) e.Tail = <MuSentence e.NextName> <DoAddMu-Names e.Tail>;
  /* пусто */ = /* пусто */;
}

MuSentence {
  e.Name
    = <MuSentence-Special e.Name>
      (((TkIdentifier e.Name) <Var-eX>) (<Call-eX e.Name>) (/* нет блоков */))
      (
        ((Brackets <Chars e.Name>) <Var-eX>)
        (<Call-eX e.Name>) (/* нет блоков */)
      );
}

Var-eX {
  = (TkVariable NoPos 'eX');
}

Call-eX {
  s.SugarName
    , ('+Add') ('-Sub') ('*Mul') ('/Div') ('%Mod') ('?Residue')
    : e.SugarNames-B (s.SugarName e.RealName) e.SugarNames-E
    = <Call-eX e.RealName>;

  e.Name = (CallBrackets (TkName NoPos e.Name) <Var-eX>);
}

MuSentence-Special {
  s.Name, '+-*/%?' : e.Chars-B s.Name e.Chars-E
    = (((TkChar s.Name) <Var-eX>) (<Call-eX s.Name>) (/* нет блоков */));

  e.OtherName = /* пусто */;
}

Chars {
  s.Char e.Chars = (TkChar s.Char) <Chars e.Chars>;
  /* пусто */ = /* пусто */;
}

DoAddMu-AST {
  (e.MuSentences) e.Tail (Declaration t.SrcPos GN-Entry e.Name)
    = <DoAddMu-AST (<MuSentence e.Name> e.MuSentences) e.Tail>
      (Declaration t.SrcPos GN-Entry e.Name);

  (e.MuSentences) e.Tail
  (Function t.SrcPos s.Scope (e.Name) Sentences e.Sentences)
    = <DoAddMu-AST (<MuSentence e.Name> e.MuSentences) e.Tail>
      (Function t.SrcPos s.Scope (e.Name) Sentences e.Sentences);

  (e.MuSentences) /* пусто */ = (e.MuSentences);
}
