//FROM Library
$EXTERN StrFromInt, Ord;

//FROM Escape
$EXTERN EscapeString;

//FROM LibraryEx
$EXTERN Map, Fetch;

$ENTRY DisplayName {
  e.Name #Hash s.Cookie1 s.Cookie2 =
    <DisplayName e.Name> '#' <StrFromInt s.Cookie1> ':' <StrFromInt s.Cookie2>;
  #CreateClosure = '@create_closure@';
  #VAR s.Mode e.Index s.Depth = s.Mode '.' e.Index '#' <StrFromInt s.Depth> ':';
  e.Name #SUF e.Suffix = e.Name <DisplayNameSuf e.Suffix>;
  e.Name = e.Name;
}

DisplayNameSuf {
  s.Tag s.Num e.Suffix = s.Tag <StrFromInt s.Num> <DisplayNameSuf e.Suffix>;
  /* пусто */ = /* пусто */;
}

$ENTRY DisplayCName {
  e.Name = <EscapeString <DisplayName e.Name>>;
}

$ENTRY CName {
  e.Name #Hash s.Cookie1 s.Cookie2 =
    <CName e.Name> '_' <StrFromInt s.Cookie1> '_' <StrFromInt s.Cookie2>;
  #CreateClosure = 'refalrts::create_closure';
  #VAR s.Mode e.Index s.Depth =
    'var_' s.Mode <CName e.Index> '_' <StrFromInt s.Depth>;
  e.Name #SUF e.Suffix = 'gen_' <CName e.Name> '_' <CNameSuf e.Suffix>;
  e.Name = <DecorateChars e.Name>;
  e.Name = e.Name;
}

DecorateChars {
  e.Name
    = <Map
        {
          '-' = 'm_';
          '_' = 'u_';
          s.Other
            = <Fetch
                <AlphaNumber>
                {
                  e.AlNum-B s.Other e.AlNum-E = s.Other;
                  e.AlNum = 'k' <StrFromInt <Ord s.Other>> '_';
                }
              >;
        }
        e.Name
      >;
}

AlphaNumber {
  = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
    'abcdefghijklmnopqrstuvwxyz'
    '0123456789';
}

CNameSuf {
  '$' s.Num e.Suffix = 'S' <StrFromInt s.Num> <CNameSuf e.Suffix>;
  '\\' s.Num e.Suffix = 'L' <StrFromInt s.Num> <CNameSuf e.Suffix>;
  '=' s.Num e.Suffix = 'A' <StrFromInt s.Num> <CNameSuf e.Suffix>;
  ':' s.Num e.Suffix = 'B' <StrFromInt s.Num> <CNameSuf e.Suffix>;
  '?' s.Num e.Suffix = 'C' <StrFromInt s.Num> <CNameSuf e.Suffix>;
  /* пусто */ = /* пусто */;
}
