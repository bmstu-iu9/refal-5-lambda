$INCLUDE "LibraryEx";


/**
  <LowLevelRASL-Native s.Hash1 s.Hash2 t.Tables e.Items>
    == t.NativeModule?

  s.Hash1, s.Hash2 ::= s.NUMBER

  t.Tables ::= (t.FuncTable t.IdentTable)
  t.FuncTable, t.IdentTable ::= (s.NextId (s.Id e.Name)*)

  In e.Items is used (Function-ToRASL …) and (Function-ToNative …)
  instead (Function …).
*/
$ENTRY LowLevelRASL-Native {
  s.Hash1 s.Hash2 (t.FuncTable t.IdentTable) e.Items
    = <Map (&SetCookies s.Hash1 s.Hash2) e.Items>
    : {
        /* нет нативных определений */ = /* нет нативного RASL’а */;

        e.Definitions
          = <Map &PrepareFunctionsRASL e.Definitions> : e.DefinitionsRASL
          = (
              (CmdCookieNS s.Hash1 s.Hash2)
              <PrepareTable 'efunc' t.FuncTable>
              <PrepareTable 'ident' t.IdentTable>
              e.DefinitionsRASL
            );
      };
}

PrepareTable {
  e.Prefix (0 /* пусто */) = /* пусто */;

  e.Prefix (s.Count e.Items)
    = (CmdCEnum-Start e.Prefix)
      <Map { (s.Id e.Name) = (CmdCEnum-Item (e.Prefix) e.Name s.Id) } e.Items>
      (CmdCEnum-End);
}

SetCookies {
  s.Hash1 s.Hash2
  (Function-ToRASL s.ScopeClass (e.Name) e.Commands) =
    /* пропускаем */;

  s.Hash1 s.Hash2
  (Function-ToNative s.ScopeClass (e.Name) e.Commands) =
    (Function-ToNative
      (<MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>)
      e.Commands
    );

  s.Hash1 s.Hash2
  (CmdNativeFunction s.ScopeClass (e.Name) t.SrcPos e.Code) =
    (CmdNativeFunction
      (<MingleName s.ScopeClass e.Name s.Hash1 s.Hash2>)
      t.SrcPos e.Code
    );

  s.Hash1 s.Hash2 (CmdEnum s.ScopeClass e.Name) =
    /* пропускаем */;

  s.Hash1 s.Hash2 (CmdSwap s.ScopeClass e.Name) =
    /* пропускаем */;

  s.Hash1 s.Hash2
  (CmdConditionFunc-ToRASL s.ScopeClass e.Name) =
    /* пропускаем */;

  s.Hash1 s.Hash2
  (CmdConditionFunc-ToNative s.ScopeClass e.Name) =
    /* пропускаем */;

  s.Hash1 s.Hash2 (CmdDeclaration s.ScopeClass e.Name) =
    /* пропускаем */;

  s.Hash1 s.Hash2 (CmdEmitNativeCode t.SrcPos e.Code) =
    (CmdEmitNativeCode t.SrcPos e.Code);
}

MingleName {
  GN-Entry e.Name s.Hash1 s.Hash2 = e.Name Hash 0 0;
  GN-Local e.Name s.Hash1 s.Hash2 = e.Name Hash s.Hash1 s.Hash2;
}

PrepareFunctionsRASL {
  (Function-ToNative (e.Name) e.Body)
    = (CmdFnStart e.Name)
      (CmdProfileFunction)
      (CmdLoadConstants)
      <ConvertDirectCommands e.Body>
      (CmdFnEnd)
      (CmdNativeFuncDescr e.Name);

  (CmdNativeFunction (e.Name) t.SrcPos e.Code)
    = (CmdFnStart e.Name)
      (CmdLoadConstants)
      (CmdEmitNativeCode t.SrcPos e.Code)
      (CmdFnEnd)
      (CmdNativeFuncDescr e.Name);

  (CmdEmitNativeCode t.SrcPos e.Code)
    = (CmdEmitNativeCode t.SrcPos e.Code);
}

ConvertDirectCommands {
  (CmdSentence e.SubCommands) e.Tail
    = (CmdStartSentence)
      <ConvertDirectCommands e.SubCommands>
      (CmdEndSentence)
      <ConvertDirectCommands e.Tail>;

  (CmdOpenELoop AlgLeft s.BracketNum s.VarNumber e.SubCommands) e.Tail
    = (CmdOpenedE-Start AlgLeft s.BracketNum s.VarNumber)
      <ConvertDirectCommands e.SubCommands>
      (CmdOpenedE-End AlgLeft s.BracketNum s.VarNumber)
      <ConvertDirectCommands e.Tail>;

  (CmdVariableDebugTable s.Mode e.Index s.Depth s.Offset) e.Tail
    = (CmdComment
        'DEBUG: ' s.Mode '.' e.Index '#' <Symb s.Depth> ': ' <Symb s.Offset>
      )
      <ConvertDirectCommands e.Tail>;

  t.Command e.Tail
    = t.Command <ConvertDirectCommands e.Tail>;

  /* пусто */ = /* пусто */;
}
