$INCLUDE "LibraryEx";


*$FROM GetOpt
$EXTERN GetOpt;

*$FROM Version
$EXTERN PrintVersion;


/**
  <ParseCommandLine (e.Option)*>
    == InPlace e.FileName
    == From (e.FromFileName) To (e.ToFileName)
*/
$ENTRY ParseCommandLine {
  e.Options
    = <GetOpt
        (
          (From Required 'f' ('from'))
          (To Required 'o' ('to'))
          (Help None ('help'))
          (Version None ('version'))
        )
        e.Options
      >
    : (e.Errors) e.Options^
    = (e.Errors)
      <Map
        {
          (Help s.Num) = <Help> <Exit 0>;

          (Version s.Num) = (Version s.Num) <PrintVersion 'LexGen'>;

          t.Other = t.Other;
        }
        e.Options
      >
    : {
        () (FILE s.Pos e.FileName) =
          InPlace e.FileName;

        () e.Begin (From s.PosFrom e.From) e.End
          = <SkipVersion e.Begin e.End>
          : {
              (To s.PosTo e.To) =
                From (e.From) To (e.To);

              e.Other =
                <FormatError s.PosFrom 'expected argument --to'>
                <HelpOnError>;
            };

        () e.Begin (To s.Pos e.To) e.End =
          <FormatError s.Pos 'exptected argument --from'>
          <HelpOnError>;

        () (Version s.Num) e.VersionFlags
          , <SkipVersion e.VersionFlags> : /* пусто */
          = <Exit 0>;

        () e.AnyOther =
          <Prout 'Command line error: unrecognized command line'>
          <HelpOnError>;

        (e.Errors) e.AnyOther =
          <Map
            {
              (s.Pos NoRequiredParam e.Opt) =
                <FormatError s.Pos 'option ' e.Opt ' expects parameter'>;

              (s.Pos UnknownShortOption s.Option) =
                <FormatError s.Pos 'unknown option -' s.Option>;

              (s.Pos UnknownLongOption e.Option) =
                <FormatError s.Pos 'unknown option --' e.Option>;
            }
            e.Errors
          >
          <HelpOnError>;
      }
}

SkipVersion {
  e.Options-B (Version s.Num) e.Options-E = <SkipVersion e.Options-B e.Options-E>;

  e.Options = e.Options;
}


FormatError {
  s.Pos e.Message =
    <Prout 'Command line argument ' <Symb s.Pos> ': ' e.Message>;
}

Help {
  =
    <Prout 'Use:'>
    <Prout '    lexgen filename.sref - for rewritting file\n'>
    <Prout '    lexgen --from=file1.sref --to=file2.sref - for writting '
               'to other target\n'>
    <Prout '    --from, -f - source file'>
    <Prout '    --to, -o - target file'>
    <Prout '    --help - print this help'>
    <Prout '    --version - print version'>
}

HelpOnError {
  = <Help> <Exit 1>
}
