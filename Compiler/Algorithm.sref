// FROM Algorithm_v1
$EXTERN MakeAlgorithm_v1;

// FROM Algorithm_v2
$EXTERN MakeAlgorithm_v2;

// FROM Algorithm_v1
$EXTERN CmdIfDef, CmdElse, CmdEndIf;	

// FROM LibraryEx
$EXTERN Map;

$EENUM LeftPtr, RightPtr;

$ENTRY MakeAlgorithm {
  (e.Pattern) (e.Result) =
    (CmdIfDef 'OPTIMIZE_RESULT')
    <MakeAlgorithm_v2 (e.Pattern) (e.Result)>
    (CmdElse)
    <MakeAlgorithm_v1 (e.Pattern) (e.Result)>
    (CmdEndIf);
}

$ENTRY VarSetUnion {
  ( e.Set1-B t.Common e.Set1-E ) ( e.Set2-B t.Common e.Set2-E ) =
    t.Common
    <VarSetUnion (e.Set1-B e.Set1-E) (e.Set2-B e.Set2-E)>;

  ( e.Set1 ) ( e.Set2 ) = e.Set1 e.Set2;
}

$ENTRY VarSetDifference {
  ( e.Set1-B t.Common e.Set1-E ) ( e.Set2-B t.Common e.Set2-E ) =
    e.Set1-B <VarSetDifference (e.Set1-E) ( e.Set2-B e.Set2-E )>;

  ( e.Set1 ) ( e.Set2 ) = e.Set1;
}

$EENUM CmdDeclareEVar, CmdDeclareVar;

$ENTRY MakeDeclaration {
  ( s.Usings 'e' e.Index ) = (CmdDeclareEVar s.Usings 'e' e.Index);

  ( s.Usings s.Mode e.Index ) =
    (CmdDeclareVar s.Usings s.Mode e.Index);
}

//FROM LibraryEx
$EXTERN Dec;

ReplicateVar {
  (1 s.Mode e.Index) = (1 s.Mode e.Index);

  (s.Using s.Mode e.Index) =
    <ReplicateVar (<Dec s.Using> s.Mode e.Index)>
    (s.Using s.Mode e.Index);
}

$ENTRY ReplicateVars {
  e.Vars = <Map ReplicateVar e.Vars>;
}

