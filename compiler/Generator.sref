$EENUM GN-Local, GN-Entry;

FuncArguments {
  = 'refalrts::Iter arg_begin, refalrts::Iter arg_end';
}

NakedArguments {
  = 'refalrts::Iter, refalrts::Iter';
}

TextFromMemoryClass {
  GN-Local = 'static '; GN-Entry = ;
}

SwGenFnStart {
  s.MemoryClass s.FnArguments e.Name =
    (
      <TextFromMemoryClass s.MemoryClass>
      'refalrts::FnResult ' e.Name '(' <s.FnArguments> ') {'
    )
    ('  refalrts::this_is_generated_function();');
}

$ENTRY GenFnStart {
  s.MemoryClass e.Name =
    <SwGenFnStart s.MemoryClass FuncArguments e.Name>;
}

$ENTRY GenFnEnd {
  =
    ('  return refalrts::FnResult(')
    ('    refalrts::cRecognitionImpossible | (__LINE__ << 8)')
    ('  );')
    ('}') ();
}

$ENTRY GenEEnum {
  e.Name = <SwGenFnStart GN-Entry NakedArguments e.Name> <GenFnEnd>;
}

$ENTRY GenEnum {
  e.Name = <SwGenFnStart GN-Local NakedArguments e.Name> <GenFnEnd>;
}

$ENTRY GenExtern {
  e.Name =
    ('extern refalrts::FnResult ' e.Name '(' <FuncArguments> ');') ();
}

$ENTRY GenForward {
  e.Name =
    ('static refalrts::FnResult ' e.Name '(' <FuncArguments> ');') ();
}

$FORWARD GenSwap-Aux;

$ENTRY GenESwap {
  e.Name =
    ('//$ESWAP ' e.Name)
    <GenSwap-Aux GN-Entry e.Name>;
}

$ENTRY GenSwap {
  e.Name =
    ('//$SWAP ' e.Name)
    <GenSwap-Aux GN-Local e.Name>;
}

GenSwap-Aux {
  s.MemoryClass e.Name =
    <GenFnStart s.MemoryClass e.Name>
    ('  refalrts::Iter func_name = arg_begin;')
    ()
    ('  refalrts::move_left( func_name, arg_end );')
    ()
    ('  refalrts::Iter info_b = func_name;')
    ('  refalrts::Iter info_e = arg_end;')
    ()
    ('  refalrts::move_left( info_b, info_e );')
    ('  refalrts::move_right( info_b, info_e );')
    ()
    ('  static refalrts::Iter s_head = refalrts::initialize_swap_head( func_name );')
    ()
    ('  refalrts::Iter saved_b;')
    ('  refalrts::Iter saved_e;')
    ()
    ('  refalrts::swap_info_bounds( saved_b, saved_e, s_head );')
    ('  refalrts::splice_evar( arg_begin, saved_b, saved_e );')
    ('  refalrts::swap_save( s_head, info_b, info_e );')
    ('  refalrts::splice_to_freelist( arg_begin, arg_end );')
    ()
    ('  return refalrts::cSuccess;')
    ('}')
    ();
}

$ENTRY GenIdent {
  e.Name =
    ('//$LABEL ' e.Name)
    ('template <typename T>')
    ('struct ' e.Name ' {')
    ('  static const char *name() {')
    ('    return "' e.Name '";')
    ('  }')
    ('};')
    ();
}

$ENTRY GenCommonHeaders {
  = ('// Automatically generated file. Don\'t edit!')
  ('#include "refalrts.h"')
  () ();
}

$ENTRY GenCommonTailer {
  = () ('//End of file');
}

// Команды и их подтипы
//FROM Algorithm
$EXTERN CmdChar, CmdNumber, CmdName, CmdIdent,
  CmdBrackets, CmdADT, CmdEmpty,
  CmdRepeated, CmdVar, CmdClosedE,
  CmdOpenedE-Start, CmdOpenedE-End,
  CmdAllocateElem, CmdLinkBrackets, CmdPushStack,
  CmdInsertElem, CmdInsertEVar,
  ElChar, ElName, ElNumber, ElIdent,
  ElOpenADT, ElCloseADT,
  ElOpenBracket, ElCloseBracket,
  ElOpenCall, ElCloseCall,
  ElElem, ElSTVar,
  CmdEmptyResult, CmdReturnResult,
  CmdIfDef, CmdElse, CmdEndIf,
  CmdInitRAA, CmdFinRAA,
  CmdDeclareEVar, CmdDeclareVar,
  CmdCopyEVar, CmdCopyVar,
  AlgLeft, AlgRight,
  CmdComment, CmdSave,
  CmdArrChar, CmdArrInt, CmdArrFunc, CmdArrIdent,
  CmdArrOB, CmdArrCB, CmdArrOADT, CmdArrCADT,
  CmdArrOC, CmdArrCC, CmdArrSplice, CmdArrCopy;

$FORWARD GenCommand, BracketsVar-B, BracketsVar-E, BracketsVars;

//FROM LibraryEx
$EXTERN Map, MapReduce, Fetch;

//FROM Library
$EXTERN StrFromInt, WriteLine;

$ENTRY GenSentence {
  e.Algorithm =
    ('  do {')
    ('    refalrts::Iter ' <BracketsVar-B 0> ' = arg_begin;')
    ('    refalrts::Iter ' <BracketsVar-E 0> ' = arg_end;')
    ('    refalrts::move_left( ' <BracketsVars 0> ' );')
    ('    refalrts::move_left( ' <BracketsVars 0> ' );')
    ('    refalrts::move_right( ' <BracketsVars 0> ' );')
    <Fetch
      <MapReduce GenCommand ('    ' /* отступ */) e.Algorithm>
      {
        ('    ') /* отступ должен остаться тем же */
        e.Generated =
          e.Generated;
      }
    >
    ('  } while ( 0 );')
    ();
}

BracketsVar-B {
  s.BracketNumber = 'bb_' <StrFromInt s.BracketNumber>;
}

BracketsVar-E {
  s.BracketNumber = 'be_' <StrFromInt s.BracketNumber>;
}

BracketsVars {
  s.BracketNumber =
    <BracketsVar-B s.BracketNumber> ', ' <BracketsVar-E s.BracketNumber>;
}

/*
  Величина отступа также определяет режим работы. Отступ в 4 пробела --
  вне цикла по открытой e-переменной, отступ больше -- внутри оного цикла.
*/

$FORWARD PrintVar, PrintEVar-B, PrintEVar-E, PrintEVars,
  PrintMatchChar, PrintMatchNumber, PrintMatchName,
  PrintMatchIdent,
  PrintMatchADT,
  PrintMatchBrackets,
  PrintMatchEmpty,
  PrintMatchSTVar, PrintMatchRepeated,
  PrintAllocateElem, PrintElemVar;

$FORWARD Escape;

//FROM LibraryEx
$EXTERN Dec;

$LABEL CCreateClosure;

PrintName {
  # CCreateClosure = 'refalrts::create_closure';

  e.Name = e.Name;
}

GenCommand {
  (e.Indent) (CmdComment e.Text) = (e.Indent) (e.Indent '//' e.Text);

  (e.Indent) (CmdDeclareEVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'static refalrts::Iter ' <PrintEVar-B e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintEVar-B e.VarInfo> ' );')
    (e.Indent 'static refalrts::Iter ' <PrintEVar-E e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintEVar-E e.VarInfo> ' );');

  (e.Indent) (CmdDeclareVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'static refalrts::Iter ' <PrintVar e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintVar e.VarInfo> ' );');

  (e.Indent) (CmdChar s.Direction s.BracketNumber s.Char) =
    (e.Indent)
    <PrintMatchChar (e.Indent) s.Direction s.BracketNumber s.Char>;

  (e.Indent) (CmdNumber s.Direction s.BracketNumber s.Number) =
    (e.Indent)
    <PrintMatchNumber (e.Indent) s.Direction s.BracketNumber s.Number>;

  (e.Indent) (CmdName s.Direction s.BracketNumber e.Name) =
    (e.Indent)
    <PrintMatchName (e.Indent) s.Direction s.BracketNumber e.Name>;

  (e.Indent) (CmdIdent s.Direction s.BracketNumber e.Name) =
    (e.Indent)
    <PrintMatchIdent (e.Indent) s.Direction s.BracketNumber e.Name>;

  (e.Indent) (CmdBrackets s.Direction s.BracketNumber s.InnerBrackets) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar-B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar-E s.InnerBrackets> ' = 0;')
    <PrintMatchBrackets (e.Indent) s.Direction s.BracketNumber s.InnerBrackets>;

  (e.Indent) (CmdADT s.Direction s.BracketNumber s.InnerBrackets e.Name) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar-B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar-E s.InnerBrackets> ' = 0;')
    <PrintMatchADT
      (e.Indent) s.Direction s.BracketNumber s.InnerBrackets e.Name
    >;

  (e.Indent) (CmdEmpty s.Direction s.BracketNumber) =
    (e.Indent)
    <PrintMatchEmpty (e.Indent) s.BracketNumber>;

  (e.Indent) (CmdVar s.Direction s.BracketNumber s.Mode e.Info) =
    (e.Indent)
    <PrintMatchSTVar (e.Indent) s.Direction s.BracketNumber s.Mode e.Info>;

  (e.Indent) (CmdRepeated s.Direction s.BracketNumber e.VarInfo) =
    (e.Indent)
    <PrintMatchRepeated (e.Indent) s.Direction s.BracketNumber e.VarInfo>;

  (e.Indent) (CmdClosedE AlgLeft s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      <PrintEVar-B 1 'e' e.Index> ' = ' <BracketsVar-B s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar-B 1 'e' e.Index> ' );')
    (e.Indent
      <PrintEVar-E 1 'e' e.Index> ' = ' <BracketsVar-E s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar-E 1 'e' e.Index> ' );');

  (e.Indent)
  (CmdOpenedE-Start AlgLeft s.BracketNumber 'e' e.Index) =
    (e.Indent '  ')
    (e.Indent <PrintEVar-B 1 'e' e.Index> ' = 0;')
    (e.Indent <PrintEVar-E 1 'e' e.Index> ' = 0;')
    (e.Indent 'do {');

  (e.Indent '  ')
  (CmdOpenedE-End AlgLeft s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '} while ( refalrts::open_evar_advance( '
      <PrintEVars 1 'e' e.Index> ', ' <BracketsVars s.BracketNumber>
      ' ) );'
    );

  (e.Indent) (CmdSave s.OldBracketNumber s.NewBracketNumber) =
    (e.Indent)
    (e.Indent
      'refalrts::Iter ' <BracketsVar-B s.NewBracketNumber>
      ' = ' <BracketsVar-B s.OldBracketNumber> ';'
    )
    (e.Indent
      'refalrts::Iter ' <BracketsVar-E s.NewBracketNumber>
      ' = ' <BracketsVar-E s.OldBracketNumber> ';'
    );

  (e.Indent) (CmdEmptyResult) =
    (e.Indent)
    ()
    (e.Indent 'refalrts::reset_allocator();')
    (e.Indent 'refalrts::Iter res = arg_begin;');

  (e.Indent) (CmdCopyEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_evar( ' <PrintEVars s.Usings 'e' e.Index> ', '
      <PrintEVars <Dec s.Usings> 'e' e.Index> ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdCopyVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_stvar( ' <PrintVar s.Usings s.Mode e.Index> ', '
      <PrintVar <Dec s.Usings> s.Mode e.Index> ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdAllocateElem s.ElemNumber e.ElemInfo) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintElemVar s.ElemNumber> ' = 0;')
    <PrintAllocateElem (e.Indent) s.ElemNumber e.ElemInfo>;

  (e.Indent) (CmdLinkBrackets s.Left s.Right) =
    (e.Indent)
    (e.Indent
      'refalrts::link_brackets( '
      <PrintElemVar s.Left> ', ' <PrintElemVar s.Right>
      ' );'
    );

  (e.Indent) (CmdPushStack s.ElemNumber) =
    (e.Indent)
    (e.Indent 'refalrts::push_stack( ' <PrintElemVar s.ElemNumber> ' );');

  (e.Indent) (CmdInsertElem ElElem s.ElemNumber) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_elem( res, ' <PrintElemVar s.ElemNumber> ' );'
    );

  (e.Indent) (CmdInsertElem ElSTVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_stvar( res, '
      <PrintVar s.Usings s.Mode e.Index> ' );'
    );

  (e.Indent) (CmdInsertEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_evar( res, '
      <PrintEVars s.Usings 'e' e.Index> ' );'
    );

  (e.Indent) (CmdReturnResult) =
    (e.Indent)
    (e.Indent 'refalrts::use( res );')
    (e.Indent 'refalrts::splice_to_freelist( arg_begin, arg_end );')
    (e.Indent 'return refalrts::cSuccess;');

  (e.Indent) (CmdIfDef) =
    (e.Indent) ('#ifdef INTERPRET');

  (e.Indent) (CmdElse) = 
    (e.Indent) ('#else');

  (e.Indent) (CmdEndIf) = 
    (e.Indent) ('#endif');

  (e.Indent) (CmdInitRAA) = 
    (e.Indent '  ')
    (e.Indent 'const static refalrts::ResultAction raa[] = {');

  (e.Indent '  ') (CmdFinRAA) = 
    (e.Indent)
    (e.Indent '  {refalrts::icEnd}')
    (e.Indent '};')
    (e.Indent
      'refalrts::Iter allocs[2*sizeof(raa)/sizeof(raa[0])];'
    )
    (e.Indent
      'refalrts::FnResult res = '
      'refalrts::interpret_array( raa, allocs, arg_begin, arg_end );'
    )
    (e.Indent 'return res;');

  (e.Indent) (CmdArrChar s.Value) =
    (e.Indent)
    (e.Indent '{refalrts::icChar, 0, 0, \'' <Escape s.Value> '\'},');

  (e.Indent) (CmdArrFunc e.Name) =
    (e.Indent)
    (e.Indent
      '{refalrts::icFunc, '
      '(void*) & ' <PrintName e.Name> ', '
      '(void*) "' <PrintName e.Name> '"},'
    );

  (e.Indent) (CmdArrInt s.Number) =
    (e.Indent)
    (e.Indent '{refalrts::icInt, 0, 0, ' s.Number '},');

  (e.Indent) (CmdArrIdent e.Name) =
    (e.Indent)
    (e.Indent '{refalrts::icIdent, (void*) & ' e.Name '<int>::name},');

  (e.Indent) (CmdArrOB) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenBracket},');

  (e.Indent) (CmdArrCB) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseBracket},');

  (e.Indent) (CmdArrOADT) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenADT},');

  (e.Indent) (CmdArrCADT) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseADT},');

  (e.Indent) (CmdArrOC) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenCall},');

  (e.Indent) (CmdArrCC) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseCall},');

  (e.Indent) (CmdArrCopy 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icCopyEVar, '
      '& ' <PrintEVar-B 1 'e' e.Index> ', '
      '& ' <PrintEVar-E 1 'e' e.Index> '},'
    );

  (e.Indent) (CmdArrCopy s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icCopySTVar, & ' <PrintVar 1 s.Mode e.Index> '},'
    );

  (e.Indent) (CmdArrSplice s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icSpliceEVar, '
      '& ' <PrintEVar-B s.Usings 'e' e.Index> ', '
      '& ' <PrintEVar-E s.Usings 'e' e.Index> '},'
    );

  (e.Indent) (CmdArrSplice s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icSpliceSTVar, & ' <PrintVar s.Usings s.Mode e.Index> '},'
    );
}

PrintVar {
  s.Usings s.Mode e.Index s.Depth =
    s.Mode e.Index '_' <StrFromInt s.Depth> '_' <StrFromInt s.Usings>;
}

PrintEVar-B {
  s.Usings s.Mode e.Index s.Depth =
    s.Mode e.Index '_' <StrFromInt s.Depth> '_b_' <StrFromInt s.Usings>;
}

PrintEVar-E {
  s.Usings s.Mode e.Index s.Depth =
    s.Mode e.Index '_' <StrFromInt s.Depth> '_e_' <StrFromInt s.Usings>;
}

PrintEVars {
  s.Usings s.Mode e.Index =
    <PrintEVar-B s.Usings s.Mode e.Index> ', '
    <PrintEVar-E s.Usings s.Mode e.Index>;
}

$FORWARD PrintMatch;

Escape {
  '\n' = '\\n';
  '\t' = '\\t';
  '\r' = '\\r';
  '\\' = '\\\\';
  '' = '\\' '';
  s.Other = s.Other;
}

PrintMatchChar {
  (e.Indent) s.Direction s.BracketNumber s.Char =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('char') '' <Escape s.Char> ''
    >;
}

PrintMatchNumber {
  (e.Indent) s.Direction s.BracketNumber s.Number =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('number') <StrFromInt s.Number> 'UL'
    >;
}

PrintMatchName {
  (e.Indent) s.Direction s.BracketNumber e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('function') '& ' e.Name
    >;
}

PrintMatchIdent {
  (e.Indent) s.Direction s.BracketNumber e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('ident') ' & ' e.Name '<int>::name'
    >;
}

PrintMatchBrackets {
  (e.Indent) s.Direction s.BracketNumber s.InnerNum =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('brackets')
      <BracketsVars s.InnerNum>
    >;
}

PrintMatchADT {
  (e.Indent) s.Direction s.BracketNumber s.InnerNum e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('adt')
      <BracketsVars s.InnerNum> ', '
      '& ' e.Name
    >;
}

BreakerFromIndent {
  '    ' = 'break;';

  e.OtherIndent = 'continue;';
}

PrintMatchEmpty {
  (e.Indent) s.BracketNumber =
    (e.Indent
      'if( ! refalrts::empty_seq( ' <BracketsVars s.BracketNumber> ' ) )'
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);
}

StrFromDirection {
  AlgLeft = 'left'; AlgRight = 'right';
}

VarMatchFunction { 's' = 'svar'; 't' = 'tvar'; }

PrintMatchSTVar {
  (e.Indent) s.Direction s.BracketNumber s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      (<VarMatchFunction s.Mode>) <PrintVar 1 s.Mode e.Index>
    >;
}

PrintMatchRepeated {
  (e.Indent) s.Direction s.BracketNumber s.Usings 'e' e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_evar')
      <PrintEVars s.Usings 'e' e.Index> ', '
      <PrintEVars <Dec s.Usings> 'e' e.Index>
    >;

  (e.Indent) s.Direction s.BracketNumber s.Usings s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_stvar') <PrintVar s.Usings s.Mode e.Index>
      ', ' <PrintVar <Dec s.Usings> s.Mode e.Index>
    >;
}

PrintMatch {
  (e.Indent) s.Direction s.BracketNumber (e.Function) e.TextRep =
    (e.Indent
      'if( ! refalrts::' e.Function '_' <StrFromDirection s.Direction>
      '( ' e.TextRep ', ' <BracketsVars s.BracketNumber> ' ) )'
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);
}

PrintElemVar {
  s.Number = 'n' <StrFromInt s.Number>;
}

$FORWARD PrintAllocate-Aux, SwAllocator, SwInfo;

PrintAllocateElem {
  (e.Indent) s.ElemNumber s.ElementType e.Info =
    <PrintAllocate-Aux
      (e.Indent) s.ElemNumber
      ( <SwAllocator s.ElementType> )
      ( <SwInfo s.ElementType e.Info> )
    >
    (e.Indent '  return refalrts::cNoMemory;');
}

PrintAllocate-Aux {
  (e.Indent) s.ElemNumber (e.Constructor) (e.Value) =
    (e.Indent
      'if( ! refalrts::' e.Constructor '( ' <PrintElemVar s.ElemNumber>
      e.Value ' ) )'
    );
}

SwAllocator {
  ElChar = 'alloc_char';
  ElName = 'alloc_name';
  ElNumber = 'alloc_number';
  ElIdent = 'alloc_ident';

  ElOpenADT = 'alloc_open_adt';
  ElCloseADT = 'alloc_close_adt';

  ElOpenBracket = 'alloc_open_bracket';
  ElCloseBracket = 'alloc_close_bracket';

  ElOpenCall = 'alloc_open_call';
  ElCloseCall = 'alloc_close_call';
}

SwInfo {
  ElChar s.Char = ', ' '' <Escape s.Char> '';

  ElName e.Name = ', & ' <PrintName e.Name> ', "' <PrintName e.Name> '"';

  ElNumber s.Number = ', ' <StrFromInt s.Number> 'UL';

  ElIdent e.Name = ', & ' e.Name '<int>::name';

  // Остальное -- скобки
  s.Other = ;
}
